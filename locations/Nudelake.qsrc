# Nudelake

$loc = 'Nudelake'
$metka = ''
$menu_loc = 'Nudelake'
$menu_arg = ''
menu_off = 0
$location_type = 'secluded'

'<center><b>nudist beach</b></center>'
'<center><img <<$set_imgh>> src="images/locations/city/residential/lake/nudelake.jpg"></center>'
'The nudist beach - clothing optional.'

if $clothingworntype = 'swimwear' or $clothingworntype = 'nude':
	act'Get dressed':
		cla
		*clr
		minut+=5
		gs 'clothing', 'wear_last_worn'
		gs 'underwear', 'wear'
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/secluded_beach/dress/dress_nudebeach.jpg"></center>'
		'You put your clothes back on.'
		act'Continue':gt'Nudelake'
	end
end

if temper >= 15 and month >= 5 and month <= 9:
	if ARRSIZE('swimwear') > 0 and $clothingworntype ! 'swimwear':
		act'Change into your swimwear':
			menu_off = 1
			cla
			*clr
			minut+=5
			if pcs_horny >= 90:
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/secluded_beach/self/wetpussypants1.jpg"></center>'
				'You want to change into a swimsuit, but with no changing room here and your pussy oozing juices down your leg coupled with the thought that people will see, horrifies you. You abandon the idea.'
				'"I should sort out my arousal first." You think to yourself.'

				act'Continue':gt'Nudelake'
			else
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/secluded_beach/dress/swimsuit_nudebeach.jpg"></center>'
				'You wrap a towel around yourself and you''re ready to change into your swimsuit.'

				act'Choose outfit':gt'changingroom', 'view_swim_list'
			end
		end
	end
end

if temper >= 15 and month >= 5 and month <= 9:
	if $clothingworntype ! 'nude':
		act'Take off your clothes':
			menu_off = 1
			if pcs_inhib > 30:
				cla
				*clr
				minut += 5
				gs 'clothing', 'strip'
				gs 'underwear', 'remove'
				if pcs_horny >= 80 and exhibitionist_lvl > 1:
					'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/secluded_beach/self/wetpussypants3.jpg"></center>'
					'You strip off. A couple of passing men do a double-take and pause a moment to look at your sopping wet panties, probably thinking about what is making you so excited. You don''t care about what they might say or think of you.'
				elseif pcs_inhib >= 60:
					'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/secluded_beach/dress/undressnudebeach.jpg"></center>'
					'Not caring if anyone is looking at you, you slowly take off your clothes. You peel off each item like a stripper. Soon, you are completely naked.'
				elseif pcs_inhib < 60:
					'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/secluded_beach/dress/undressnudebeach.jpg"></center>'
					'You quickly check to see if anyone is watching, and when you''re satisfied no one is paying you undue attention, you take off your clothes. You''re naked now.'
				end
			else
				cla
				*clr
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/lake/secluded_beach/self/shyness.jpg"></center>'
				'Even though no one is around, you don''t feel confident enough to actually take your clothes off. What if someone would see you nude?'
			end
			act'Continue':gt'Nudelake'
		end
	end
end
gs 'stat'

if hour >= 6 and hour <= 20 and temper >= 15:
	if $clothingworntype = 'nude':
		'Here you can <a href="exec:gt ''Nudelake'', ''zagarat''">sunbathe</a> and <a href="exec:gt ''Nudelake'', ''swim''">swim</a>'
		act 'Go swimming': gt 'Nudelake', 'swim'
		act 'Go sunbathe': gt 'Nudelake', 'zagarat'
		'Also, you can play <a href="exec:gt ''Nudelake'', ''voleybol''">beach volleyball</a>'
	end
elseif hour >= 6 and hour <= 20:
	'Its too cold for any nudist activities.'
else
	'The nudist beach is closed.'
end

if $clothingworntype = 'nude':
	act 'Get dressed':
		if $clothingworntype = 'nude':gs 'clothing', 'wear_last_worn'
		gs 'underwear', 'wear'
	end
else
	act 'Return to the main beach':gt 'lake', 'start'
end

if katday ! daystart and npc_rel['A219'] >= 1 and temper >= 15:
	'You see Kat on the beach.'

	act 'Approach Kat':gt 'Katlake', 'kat'
end

if $ARGS[0] = 'voleybol':
	$location_type = 'event_outdoors'
	$menu_loc = 'Nudelake'
	$menu_arg = 'voleybol'
	menu_off = 0
	*clr & cla
	minut += 1
	'<center><img <<$set_imgh>> src="images/locations/city/residential/lake/voleybol.jpg"></center>'
	'You go to the volleyball court.'

	act 'Watch':gt 'Nudelake', 'smotr'
	act 'Leave':gt 'Nudelake'

	act 'Play':
		menu_off = 1
		cla
		*clr
		agil_exp += 1
		react_exp += 1
		vball_exp += rand(1,3)
		minut += 30
		inhib_exp += rand(1,3)
		'<center><img <<$set_imgh>> src="images/locations/city/residential/lake/voleybol.jpg"></center>'
		'You go up to the players and ask whether it is possible to join in, they are happy to put you on a team.'

		act 'Finish':gt 'Nudelake'
	end
end

if $ARGS[0] = 'zagarat':
	$location_type = 'event_outdoors'
	*clr & cla
	minut += 30
	if pcs_sweat < 35 : pcs_sweat += 5
	inhib_exp += rand(1,3)
	pcs_mood += 5
	menu_off = 1
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/lake/nudezagarat.jpg"></center>'

	if krem = 0:
		pcs_tan += 1
		'You go to the beach and sunbathe.'
	else
		krem -= 1
		pcs_tan += 3
		'You smear sunblock on your body and sunbathe.'
	end

	nzagrand = rand(0, 100)

	if nzagrand >= 60 and nzagrand < 70 and npc_rel['A219'] > 30 and katkey = 0:gt 'Katlake', 'key'
	if nzagrand >= 70 and nzagrand < 80 and kat = 0 and katday ! daystart:gt 'Katlake'
	if nzagrand >= 80 and nzagrand < 90:gt 'Vnlake1'
	if nzagrand >= 90:gt 'Vnlake2'

	act 'Finish':gt 'Nudelake'
end

if $ARGS[0] = 'swim':
	$location_type = 'event_outdoors'
	menu_off = 0
	cla
	*clr
	pcs_makeup = 1
	cumspclnt = 4
	gs 'cum_cleanup'
	minut += 60
	if $clothingworntype = 'nude':
		gs 'exercise', 'tier1', 1, 'stren_exp'
		gs 'exhibitionism', 2
		inhib_exp += rand(1,3)
	else
		gs 'exercise', 'tier1', 1, 'stren_exp'
		if pcs_inhib < 50:inhib_exp += rand(1,3)
	end
	pcs_sweat = 10 + rand(0,4)

	'<center><img <<$set_imgh>> src="images/locations/shared/lake/nudeswim.jpg"></center>'
	'You go for a swim.'

	act 'Finish':gt 'Nudelake'
end

if $ARGS[0] = 'smotr':
	$location_type = 'event_outdoors'
	menu_off = 0
	*clr & cla
	minut += 30
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/city/residential/lake/voleybol.jpg"></center>'
	'You watch the others play.'

	act 'Leave':gt 'Nudelake'
end

--- Nudelake ---------------------------------

