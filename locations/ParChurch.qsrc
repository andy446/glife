# ParChurch

if $ARGS[0] = 'start':
	CLOSE ALL
	if sound = 0:
		if week = 7:
			if hour >= 8 and hour <= 12: PLAY 'sound/street2.mp3',30 & PLAY 'sound/church/ivironKampan01.wav',30
			if hour >= 13 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
		elseif week = 6:
			if hour >= 19 and hour <= 23: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/church/ivironKampan01.wav',30
			if hour >= 13 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
		else
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street_leto.mp3',30
			if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
		end
	end
	$loc = 'ParChurch'
	$metka = 'start'
	$menu_loc = 'ParChurch'
	$menu_arg = 'start'
	$location_type = 'public_indoors'
	menu_off = 0
	gs 'themes', 'outdoors'
	gs 'stat'
	'<center><b><font color="maroon">Church of Saint Nikolas</font></b></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/st-nikolas.jpg"></center>'
	'Pavlovsk''s only church. The church of Saint Nikolas is the tallest building in town. It''s old, but well maintained by the worshipers of the parish.'
	''
	'You can take confession every weekday between 7h and 9h or 16h and 18h. On the weekend between 16h and 18h.'
	'Vigil is held Saturdays at 19h.'
	'The Divine Liturgy is on Sundays between 9h and 12h.'

	act 'Go to the residential area':minut += 5 & gt 'pavResidential'
	act 'Go to the commercial area':minut += 10 & gt 'pavCommercial'
	
	if week = 6 and hour >=19 and hour <= 22 and churchday ! daystart:
		act 'Attend vigil': gt 'ParChurch', 'vigil'
	elseif week = 7 and hour >= 9 and hour < 12 and churchday ! daystart:
		act 'Attend Divine Liturgy': gt 'ParChurch' , 'liturgy'
	else
		if Churchbad = 3 and Churchbadtime > daystart:
			cla & nl
			'The priest is probably still mad at you. You decide to avoid him for now.'
			act 'Go to the residential area':minut += 5 & gt 'pavResidential'
			act 'Go to the commercial area':minut += 10 & gt 'pavCommercial'
		elseif Churchbad = 3 and Churchbadtime <= daystart:
			Churchbad = 2
			*nl
			'"You figure that the priest has probably calmed down enough by now that you could talk to him."'
			act 'Enter the church': gt 'ParChurch' , 'Narthex'
		else
			act 'Enter the church': gt 'ParChurch' , 'Narthex'
		end
	end
end

if $ARGS[0] = 'vigil':
	CLOSE ALL
	*clr & cla
	menu_off = 1
	churchday = daystart
	pcs_mood += 10
	temprand = rand(0,5)
	if rand(0,1) = 1:
		pcs_horny += temprand
	else
		pcs_horny -= temprand
	end
	moral += 1
	Churchbad -= 1
	minut += 30
	gs 'themes', 'indoors'
	PLAY 'sound/church/Blessing.mp3',30
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/church'+ rand(1,5) +'.jpg"></center>'
	'You attend the vigil and reflect on the recent events in your life. The harmonies as the hymns and psalms are sung sweep aside some of the worries in your heart, although you know they will return once you leave this holy place, you still enjoy the rest. The sweet scent of incense tickles your nose and reminds you of the scent of' + iif(rex_love = 1, 'Rex', 'your step-father') + '''s cologne.'  
	if pav_slut >= 125:
		'Most of the parishioners seem upset that you are here. You notice that parents are making sure their children don''t go near you. Even a couple of the acolytes glare at you angrily.'
	elseif pav_slut >= 100:
		'A number of poeple give you dirty looks and intentionally turn away from you whenever you make eye contact.'
	elseif pav_slut >= 75:
		'You notice some of the old women looking at you with disapproval and one or two of the old men looking at you with interest.'
	end
	gs 'stat'

	act 'Return to Narthex': gt 'ParChurch', 'Narthex'
	act 'Stay for the Rite of Mutual Forgiveness':
		*clr & cla
		pcs_mood += 10
		pcs_horny -= 20
		moral += 1
		Churchbad -= 1
		minut += 10
		gs 'stat'
		gs 'ParChurch', 'reducepavrep', 10, -2

		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/church'+ rand(9,10) +'.jpg"></center>'
		'You decide to stay for the rite of mutual forgiveness and feel much better afterwards.'
		act 'Return to Narthex': gt 'ParChurch', 'Narthex'
	end
end

if $ARGS[0] = 'liturgy':
	*clr & cla
	CLOSE ALL
	churchday = daystart
	menu_off = 1
	minut += 60
	Churchbad -= 1
	gs 'themes', 'indoors'
	PLAY 'sound/church/t' + rand(1,19) + '.mp3',30
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/church'+ rand(1,8) +'.jpg"></center>'
	if pav_slut < 75 and Conf_Kirill <= 1: 
		pcs_mood += 20
		moral += 2
		pcs_sleep += 5
		'You join the congregation for the Divine Liturgy. You see mostly older people, but there are some youngsters. You participate in the singing but that is about all. Your presence seems to go largely unnoticed, but you have the chance to rest a bit. You feel a lot better.'
	elseif pav_slut < 75 and Conf_Kirill >= 2:
		Kirill += 1
		pcs_mood += 20
		moral += 2
		pcs_sleep += 5
		'You participate fully in the Divine Liturgy. You listen to the preaching and sing along. When going to communion and receiving the ceremonial bread, you lock eyes with Father Kirill. He seems happy to see you participating in the service.'
	else
		gs 'ParChurch', 'reducepavrep', 25, -2

		Kirill += 1
		pcs_mood +=20
		moral += 2
		'Your presence at the Divine Liturgy does not go unnoticed. People start mumbling and point fingers at you. Many wonder why you''re present at such an important and holy occasion. Father Kirill, who leads the service, seems to be confused about where the fuss comes from but then notices you. You see that even he heard about your little adventures in town, but at the same time his face doesnâ€™t seem to condemn you. It looks a bit like pity.'
		*nl
		'"The church is place for everyone, my beloved children. Even if we sometimes forget, we must try to condemn no one. Think about John 8:10: Jesus once met a woman who had slept with a man who betrayed her trust. She was dragged away by his friends and thrown naked before Jesus. The people had ripped her clothes and humiliated her. He spoke to her: ''Woman'', he says. Not ''slut'', or ''whore'', but woman. He threw His coat over her near-naked body and gave her the sense of dignity she had been promised by the man who betrayed her trust. Jesus did not condemn her, so let us try to not condemn others."'
		*nl
		'The people in the congregation seem a bit uncomfortable while hearing these words. Some look towards you and seem to have trouble looking you in the eyes. Those that do meet your eyes look away guiltily. You feel better and it seems the townsfolk might see you in a slightly better light.'	
	end
    vokal_exp += rand(0,3) 
    gs 'stat'
	
	act 'Return to Narthex': gt 'ParChurch', 'Narthex'
end

if $ARGS[0] = 'reducepavrep':
	pavsextreshold = ARGS[1]
	pavsexreduction = ARGS[2]
	if pav_sex > pavsextreshold: 
		gs 'fame', 'pav', 'sex', pavsexreduction
	else
		pav_sex = 0
	end
	if pav_prostitute > pavsextreshold:
		gs 'fame', 'pav', 'prostitute', pavsexreduction
	else
		pav_prostitute = 0
	end
end

if $ARGS[0] = 'Narthex':
	CLOSE ALL
	$menu_loc = 'ParChurch'
	$menu_arg = 'Narthex'
	menu_off = 0
	*clr & cla
	gs 'themes', 'indoors'
	gs 'stat'
	PLAY 'sound/church/76139__dkustic__090227-01-people-church-concert.wav',30
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/enterchurch.jpg"></center>'
	'You enter the church and the scent of incense and beeswax fills your lungs. Your eyes are drawn past the Narthex into the Nave with its walls covered in lovingly painted icons while a thousand points of light dance in joy throughout God''s house from the magnificent chandelier that hangs from the ceiling.'
	'To your left as you enter, there is a counter where a deacon sells candles, icons, and prayer books. He smiles and nods at you absently, acknowledging your presence without really seeing you.'
	
	act 'Leave the Church': gt 'ParChurch' , 'start'
	if ((week <= 5 and hour >= 7 and hour < 9) or (hour >= 16 and hour < 18)) and confessday ! daystart: act 'Go to confession': confessday = daystart & gt 'ParChurch2', 'confession'
	act 'Light a candle': gt 'ParChurch' , 'candle'
	act 'Pray': gt 'ParChurch' , 'pray'
	act 'Talk to the Priest': gt 'ParChurch' , 'talk_priest'
end

if $ARGS[0] = 'candle':
	*clr & cla
	minut += 10
	menu_off = 1
	Churchbad -= 1
	gs 'themes', 'indoors'
	gs 'stat'
	gs 'ParChurch', 'reducepavrep', 25, -1
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_candle.jpg"></center>'
	if money >= 10 and owechurch = 0: 
		money -= 10
		'You approach the deacon selling candles and he hands you a candle in exchange for your money.'
	elseif money >= 10 and money < 20 and owechurch = 1:
		money -= 10
		pcs_mood -= 10
		'You approach the deacon selling candles and hope that he doesn''t recognize you since you couldn''t pay for your last candle. You give him 10 rubles and he hands you a candle. You hustle away from the counter while guilt gnaws at you.'
	elseif money >= 20 and owechurch = 1:
		money -= 20
		pcs_mood += 5
		'You approach the deacon selling candles. He seems confused when you hand him 20 rubles, then he smiles in understanding. You feel relieved that the debt is paid.'
	elseif money < 10: 
		owechurch = 1
		'You approach the deacon selling candles and indicate you would like a candle. When you look in your purse to pay, you realize that you have no money. The deacon sighs and hands you the candle anyway. you promise yourself to pay double next time.'
	end
	if hour > 6 and hour < 20: 
		pcs_mood += 10
		moral += 1
		'You approach the iconostasis and light your candle. It gives you a nice feeling and you say a little prayer while looking at the image of Christ the Savior.'
	else
		pcs_horny += 10
		pcs_mood += 10
		'You approach the iconostasis and light your candle in the dark church. It gives you a nice feeling and decide to say a little prayer. The darkness, combined with the pleasant sight of fire makes you feel warm inside.'
	end

	act 'Return to Narthex': gt 'ParChurch', 'Narthex'
end

if $ARGS[0] = 'pray':
	*clr & cla
	menu_off = 1
	minut += 1
	gs 'themes', 'indoors'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_praying.jpg"></center>'
	'You approach the prayer bench and kneel down to pray.'
	act 'Pray':
		*clr & cla
		menu_off = 1
		minut += 10
		Churchbad -= 1
		if pav_sex > 10: gs 'fame', 'pav', 'sex', -1
		if pav_prostitute > 10: gs 'fame', 'pav', 'prostitute', -1
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_praying.jpg"></center>'
		if hour > 6 and hour < 20 and moral < 30:
			pcs_horny -= 10
			pcs_mood += 20
			moral += 1
			'You decide to pray for a while. You''re not overly religious but the moment of peace and quiet gives you the time to think through what you''re doing with your life.'
		elseif hour > 6 and hour < 20 and moral >= 30 and moral < 60:
			pcs_horny -= 20
			pcs_mood += 30
			moral += 1
			minut += 15
			'You decide to pray for a while. While you don''t consider yourself to be deeply religious, you do feel a strong connection to God when you look at the icons around you. You allow yourself to look deeply into your soul as you contemplate the direction your life is going.'
		elseif hour >6 and hour < 20 and moral >= 60:
			pcs_horny -= 30
			pcs_mood += 40
			moral += 1
			minut += rand(30,60)
			'You decide to pray. The beauty and glory of being in the body of God overwhelms you and you lose yourself in prayer. You are unsure of how much time has past when you finally become aware of your surroundings again, but it doesn''t matter as you are at peace with God and the world.'
		elseif (hour >= 20 or hour <= 6) and moral < 30:
			pcs_horny -= 40
			pcs_mood += 50
			moral += 1 
			minut += (60 - randtime)
			'The Nave is lit only with small candles, leaving your surroundings in darkness. You start to get a weird vibe, one you can''t quite shake. Suddenly you get the urge to pray very intensely. Before you know it, an hour went by and you feel a bit reborn.'
			'"That felt refreshing," you mumble to yourself.'
		elseif (hour >= 20 or hour <= 6) and moral >= 30 and moral < 60:
			pcs_horny = 0
			pcs_mood += 50
			moral += 1 
			if randtime > 60: randtime = 60
			minut += (60 - randtime)
			if nark > 1: nark = nark/2
			'The Nave is lit only with small candles, leaving your surroundings in darkness, but you find it comforting. You get the urge to pray very intensely. Before you know it, an hour went by and you feel a bit reborn and cleansed.'
			'"That felt refreshing," you mumble to yourself.'
		elseif (hour >= 20 or hour <= 6) and moral >= 60 and rand(1,100) > 90:
			pcs_horny = 50
			orgasm += 1
			pcs_mood = 100
			moral += 1
			Godslut = 1
			'The Nave is lit only with small candles, leaving your surroundings in darkness, but it''s comforting, like being in the womb of God. You get the urge to pray very intensely. You lose yourself in the presence of God and are overcome by religious ecstasy. You fight back a sob as your body trembles with an uncontrollable orgasm. You wipe the tears from your eyes and gaze with adoration at the face of Christ, your newest lover.'
		else
			pcs_horny = 0
			pcs_mood = 100
			moral += 1 
			minut += 60
			if nark > 1: nark = ((nark/4)*3)
			'The Nave is lit only with candles and small presence lights and the darkness feels very comforting, like being in the womb. You get the urge to pray very intensely. Before you know it, more than an hour went by and you feel a bit reborn and cleansed.'
			'"That felt refreshing," you mumble to yourself.'	
		end
		gs 'stat'
		act 'Return to Narthex': gt 'ParChurch', 'Narthex'
	end

	if exhibitionist_lvl > 0:
		act 'Be naughty':
			*clr & cla
			menu_off = 1
			Churchbad += 2
			gs 'fame', 'pav', 'sex', 2
			gs 'exhibitionism', 2
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_prayingflash.jpg"></center>'
			'While you''re praying, you get a devious idea. With a sly smile, you expose your butt, giving anyone that looks your way the perfect view of your ass. Finally, you lower your head and pray in silence. Occasionally you can hear some whispers behind you but it only makes you smile.'
			act 'Return to Narthex': gt 'ParChurch', 'Narthex'
		end
	end
end

if $ARGS[0] = 'talk_priest':
	*clr & cla
	menu_off = 1
	minut += 15
	gs 'themes', 'indoors'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_talk.jpg"></center>'
	'As you approach the priest, you recall your previous behavior, your sins, your transgressions, and the daily temptations you face.'
	'Going up to the priest, you bow down, touching the floor with your right hand then place your right hand over the left with your palms upward and say, "Bless me father."'
	'"May the Lord bless you," the priest answers and blesses you with the Sign of the Cross then places his right hand in your hands.' 
	'You kiss his hand and then straighten taking a deep breath.'
	if Churchbad > 0 and amends_day ! daystart:
		act 'Make Amends':
			*clr & cla
			gs 'stat'
			moral += 1
			Kirill += 1
			Churchbad -= 1
			amends_day = daystart
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_talk.jpg"></center>'
			'"Father, I want to tell you how sorry I am for the way I acted the last time I was here," you apologize, hoping he can hear the sincerity in your voice. "It was inexcusable."'
			'"Your behavior was indeed... ill-fitting of a young lady," the priest replies gravely. "However, God asks us to forgive... If you truly wish to make amends, there is something you could do."'
			'"Of course, father," you reply, eager for forgiveness.'
			act 'What do you need me to do':
				*clr & cla
				gs 'stat'
				if Churchbad <= 2:
					*clr & cla
					'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/supplycloset.jpg"></center>'
					'The priest leads you back to the Narthex and opens a door on the side opposite the counter where you buy candles. Inside you see cleaning supplies.'
					'Taking out a broom, the priest smiles at you, "The Narthex and the Sanctuary could use a good sweeping and mopping," he says.'
					act 'Start cleaning':
						*clr & cla
						minut += 60
						Churchbad -= 1
						moral += 1
						gs 'stat'
						'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/churchclean.jpg"></center>'
						'You take the broom and begin your task, thankful that the church isn''t a very large one. You diligently sweep the floor, making sure to get into all the corners. Once the sweeping is done, you mop the floor then put everything away. You feel a sense of wellbeing at a job well done.'
						'As you close the closet door, you turn around to find the priest looking at you with a pleased expression.'
						'"Thank you, <<$pcs_nickname>>. You did a fine job." he says. Surprisingly, you feel a flush of pleasure at the praise. You finished putting things away and leave.'
						act 'Leave the church': gt 'ParChurch' , 'start'
					end
					act 'Tell him you''re not a maid':
						*clr & cla
						minut += 5
						Churchbad += 2
						Kirill -= 5 
						moral -= 2
						gs 'stat'
						'<center><img <<$set_imgh>> src="images/pc/bukli_angry.jpg"></center>'
						'"As if," you state flatly, looking at the priest coldly, "You''re the one that took an oath to work for God, not me. I''m out of here."'
						'The priest looks as if he was going to say something then changed his mind. He shakes his head sadly as you walk away.'
						act 'Leave the church': gt 'ParChurch' , 'start'
					end
				else
					cla
					'The priest leads you to a bench in the Narthex and has you sit down with him.'
					'"I feel that you have lost your way, <<$pcs_nickname>>," he begins softly. "Such behavior can only lead to a life of misery and sin."'
					'"I know father," you reply with a sigh. "But I just can''t seem to help it sometimes."'
					act 'Tease him': gt 'ParChurch', 'tease'
				end
			end
		end
	elseif Churchbad > 0 and amends_day = daystart:
		'<br>You already made amends today.'
	end
	act 'Talk to the priest':
		*clr & cla
		moral += rand(0,3)
		Kirill += rand(0,2)
		minut += 15
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_talk.jpg"></center>'
		'You sit and talk with the priest about morality and how Russian society has changed since the fall of the Soviet Union. You get the impression that although the priest was happy to see the church gain wider acceptance and freedom from the government, he doesn''t seem to approve of how today''s youth behave.'
		act 'Leave the church': gt 'ParChurch' , 'start'
		act 'Stay in the Narthex': gt 'ParChurch', 'Narthex'
		act 'Keep talking':
			*clr & cla
			moral += rand(0,2)
			Kirill += rand(0,2)
			minut += 30
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/ch_talk.jpg"></center>'
			'You are enjoying your discussion with him and turn to talking about Pavlovsk and about how so many people seem obsessed with sex, especially the boys. He agrees with you that people seem overly lustful around this area. Soon you are discussing about how society has brought on these loser morals in people.'
			'You see an old woman you know vaguely from the apartment block next to yours. She seems anxious to talk to the priest.'
			if Kirill > 60:
				'"It looks like Mrs. Seldipi needs to regal me with the latest of her neighbors'' multitude of sins," the priest confides with a wry smile, "We can continue our discussion another day."'
				act 'Leave the church': gt 'ParChurch' , 'start'
			else
				'"I am sorry, <<$pcs_nickname>>. It seems that I am needed by Mrs. Seldipi," he says with obvious regret. "I''ve enjoyed our chat. Please think on what we''ve discussed and we will talk another time."'
				act 'Leave the church': gt 'ParChurch' , 'start'
			end
		end
		act 'Tease him': gt 'ParChurch', 'tease'
	end
	act 'Chicken out': 
		*clr & cla
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/enterchurch.jpg"></center>'
		'You could not bring yourself to talk to the priest yet. You were torn between stalling or simply leaving.'
		act 'Leave the church': gt 'ParChurch' , 'start'
		act 'Stay in the Narthex': gt 'ParChurch', 'Narthex'
	end
end

if $ARGS[0] = 'tease':
	*clr & cla
	Kirill_Crpt += 1
	moral -= 1
	Churchbad += 1
	'<center><img <<$set_imgh>> src="images/shared/sex/exhibitionism/fatherim30.jpg"></center>'
	'Feeling naughty, you decide to try and tease the priest. While he isn''t looking, you undo most of the buttons on your blouse, hoping he will notice.'
	if Kirill_Crpt >= 30:
		Kirill += 1
		Kirill_Crpt += 1
		pcs_horny += 5
		'You look the priest right in the eyes then when his attention is on you, you slowly lean forward, allowing your breasts to be exposed. To your delight, you see his eyes dart to your tits and stay there.'
		if kirillsex ! 0:
			'"Very nice, <<$pcs_nickname>>." Father Kirill whispers as he reaches out and caresses your firm breasts before pulling your top back into place, covering you once again. "Follow me."'
			act 'Follow him': gt 'ParChurch2', 'sex'
			gs 'willpower', 'sex', 'resist'
			if will_cost <= pcs_willpwr:
				act 'Leave the church (<<will_cost>> Willpower)':
					gs 'willpower', 'pay', 'resist'
					gs 'stat'
					gt 'ParChurch' , 'start'
				end
			else
				'<br>You don''t have enough willpower to refuse.'
			end
		else
			'"Very nice, <<$pcs_nickname>>." Father Kirill whispers with a smile as he reaches out and softly caresses your breasts before pulling your top back into place, covering you up once again.' 
			'"No temptation has overtaken you except what is common to mankind. And God is faithful; he will not let you be tempted beyond what you can bear. But when you are tempted, he will also provide a way out so that you can endure it," he murmurs as he fastens your buttons then sighs.'
			'"Thus it is written, and thus it behooved Christ to suffer, and to rise from the dead the third day," you reply looking at the telltale lump in Father Kirill''s crotch, "It seems that Jesus is not the only one to rise from the dead," you added with a playful giggle.'
			'Father Kirill looks at you and opens his mouth to speak but no words come out.'
			'With a laugh, you jump up and skip out of the church with a final glance back at the dumbfounded cleric.'
			act 'Leave the church': gt 'ParChurch' , 'start' 
		end
	else 
		Kirill -= 2
		'You look the priest right in the eyes then when his attention is on you, you slowly lean forward, allowing your breasts to be exposed. To your delight, you see his eyes dart to your tits then snap back up to your face. His face flushes red.'
		if Churchbad >= 5: 
			'"You whore!" The priest roars in fury, "I''ll teach you what happens to those who desecrate the House of God!"'
			'Stunned by his unexpected fury, you are unable to react before the furious priest grabs you by the arm and drags you to his office.'
			act 'Continue': gt 'ParChurch', 'spank'
		elseif Churchbad <= 4 and Kirill <= 20:
			'"<<$pcs_firstname>> <<$lastname>>!" the priest hisses, his face twisted in rage, "How dare you behave like this in the House of God! Get out now!"'
			'Stunned by his unexpected fury, you hastily cover yourself up and flee from the church.'
			act 'Leave the church': gt 'ParChurch' , 'start'
		else
			'"<<$pcs_firstname>> <<$lastname>>!" the priest hisses, his face flushed with embarrassment, "How could you behave like this in the House of God! Cover yourself up."'
			'Although it isn''t the reaction you were expecting, it is a reaction. Looking up at the priest, you can tell that his embarrassment is changing to anger. You decide that it might be best if you left.'
			act 'Leave the church': gt 'ParChurch' , 'start'
		end
	end
end

if $ARGS[0] = 'spank':
	*clr & cla
	Kirill_Crpt += 1
	Churchbad -= 1
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/event/spank1.jpg"></center>'
	'Once in his office, he drags you over to in front of his desk before he lets go. "Bend over and expose your behind, you obviously need to be disciplined more to be such a sinful girl." You look at him, you know he would never force you, so why should you obey? He seems to read your thoughts as he adds. "Or I can call your mother and explain to her why her daughter is excommunicated from the church."'
	'You pale slightly at that, you don''t know what your mother would do but it would be a lot worse than getting spanked by a priest. So you do as he says and expose your but as  you bend over his desk. While you do this he grabs a shoe from behind his desk, apparently he keeps a spare set of shoes in his office.'
	gs 'arousal', 'foreplay', 3, 'exhibitionism'
	gs 'stat'
	act 'bear it':
		*clr & cla
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/event/spank2.jpg"></center>'
		'He uses the sole of his hard shoe to spank you across your ass cheeks. You can tell he isn''t putting much strength into it, it stings a little but you have had far worse. After a few minutes of this he stops and leans forward and asks. "Have you learned your lesson?"'
		gs 'pain', 1, 'asscheeks', 'spank'
		gs 'arousal', 'foreplay', 5, 'unknown'
		gs 'stat'
		act 'Yes':
			*clr & cla
			Churchbad -= 1
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/event/spank5.jpg"></center>'
			'You nod your head. "Yes, I''m sorry father. I don''t know what came over me. Please forgive me."'
			'He relaxes a bit and sighs. "I forgive you, I am sorry I had to do that. But sometimes stern measures are needed."'
			'You cover yourself back up and walk out.'
			gs 'arousal', 'foreplay', 1, 'unknown'
			gs 'arousal', 'end'
			gs 'stat'
			act 'Leave the church': gt 'ParChurch' , 'start'
		end
		act 'Not yet':
			*clr & cla
			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/event/spank3.jpg"></center>'
			'You shake your head no. "No, I don''t think I have, not when you hit like a girl." You tell him.'
			'You see his eyes flare in anger and he starts spanking you again. This time he puts a lot of force into the blows and the hard shoe sole is really starting to make your ass sting. After a few minutes of this he stops and leans forward and asks. "Now have you learned your lesson?"'
			gs 'pain', 2, 'asscheeks', 'spank'
			gs 'arousal', 'foreplay', 5, 'maso'
			gs 'stat'
			act 'Yes':
				*clr & cla
				Churchbad -= 1
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/event/spank5.jpg"></center>'
				'You nod your head. "Yes, I''m sorry father. I have seen the errors of my way. Please forgive me."'
				'He relaxes a bit and sighs. "I forgive you. I am sorry I had to do that and I am sorry it took you so long to learn your lesson. But sometimes stern measures are needed."'
				'You cover yourself back up and walk out a bit gingerly with your ass stinging.'
				gs 'arousal', 'foreplay', 1, 'unknown'
				gs 'arousal', 'end'
				gs 'stat'
				act 'Leave the church': gt 'ParChurch' , 'start'
			end
			act 'More, Father, more!':
				*clr & cla
				Kirill_Crpt += 1
				Churchbad += 1
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/event/spank3.jpg"></center>'
				'You cry out. "Yes father more, spank me harder please!" While sticking your butt out a bit more.'
				'You see his eyes widen slightly and his cheeks blush a bit. Again he puts puts a lot of force into the blows, the hard shoe sole is starting to make your ass feel like it is on fire. You can''t help it as a few tears roll down your cheeks. After a few minutes of this he stops and leans forward and asks. "Now have you learned your lesson?"'
				gs 'pain', 2, 'asscheeks', 'spank'
				gs 'arousal', 'foreplay', 5, 'maso'
				gs 'stat'
				act 'Yes':
					*clr & cla
					'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/event/spank5.jpg"></center>'
					'You nod your head and sniffle a bit. "Yes, I''m sorry father. The pain has purified me of my wickedness. Please forgive me."'
					'He relaxes a bit and sighs. "I forgive you. I am sorry I had to do that and but I don''t believe you are over your wicked ways. I would like you to start coming to church on a regular bases and I would like you to take confession."'
					'You cover yourself back up. "I will father, I promise." With your ass stinging like it is on fire you walk out rather stiffly.'
					gs 'arousal', 'foreplay', 1, 'unknown'
					gs 'arousal', 'end'
					gs 'stat'
					act 'Leave the church': gt 'ParChurch' , 'start'
				end
				act 'No!':
					*clr & cla
					Churchbad += 1
					'<center><img <<$set_imgh>> src="images/locations/pavlovsk/church/event/spank5.jpg"></center>'
					'You shake your head and try not to sniffle. "No Father, you will need to do more than that to beat the wickedness out of me!"'
					'He sighs and drops the shoe on the desk. "I believe you. I will pray for you and pray for guidance on how to help you. For now you may go but I would like you to start coming to church on a regular bases and I would like you to take confession."'
					'You cover yourself back up and smirk at him. "We''ll see." With your ass stinging like it is on fire you walk out rather stiffly, but maybe you will come back, it might be fun...'
					gs 'arousal', 'foreplay', 1, 'unknown'
					gs 'arousal', 'end'
					gs 'stat'
					act 'Leave the church': gt 'ParChurch' , 'start'
				end
			end
		end
	end
end

!!if $ARGS[0] = 'cemetery':
!!	'"Placeholder for cemetery"'
!!	act 'Leave the church': gt 'ParChurch' , 'start'
!!end

if $ARGS[0] = 'fillers':
	randtext = rand(0,9)
	if randtext = 0 and gsAboDance > 0 and albinaQWdance = 0:
		$safesub = 'your dance lessons'
	elseif randtext = 0 and albinaQWdance > 0 and AlbinaBlokDance = 0 and ZvezdEnd = 0:
		$safesub = 'dancing with the Starlets'
	elseif randtext = 0 and gsAboBeg > 0:
		$safesub = 'your race training'
	elseif randtext = 0 and gsAboVolley > 0 and volleysostav = 0:
		$safesub = 'playing intramural volleyball'
	elseif randtext = 0 and volleysostav > 0:
		$safesub = 'playing competitive volleyball'
	elseif randtext = 0 and month >= 9 and month < 6 and SchoolAtestat = 0:
		$safesub = 'your schoolwork'
	elseif randtext = 1:
		$safesub = 'your mother'
	elseif randtext = 2:
		$safesub = 'your stepfather'
	elseif randtext = 3:
		$safesub = 'your brother'
	elseif randtext = 4:
		$safesub = 'your sister'
	elseif randtext = 5:
		$safesub = 'your childhood friend Shulga'
	elseif randtext = 6:
		$safesub = 'trying to earn money'
	elseif randtext = 7:
		Kirill += 1
		$safesub = 'the Russian Orthodox Church'
	elseif randtext = 8 and soniaPS ! 0:
		Kirill_Crpt += 1
		$safesub = 'what Sonia has been doing'
	elseif randtext = 8 and soniaPS = 0:
		$safesub = 'your curfew on weekends'
	elseif randtext = 9:
		$safesub = 'your grandparents'
	elseif randtext = 0:
		moral += 1
		$safesub = 'God and Jesus'
	end
end

--- ParChurch ---------------------------------

