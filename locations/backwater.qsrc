# backwater

$location_type = 'secluded'
$loc = 'backwater'
$metka = ''
$menu_loc = 'backwater'
$menu_arg = ''
menu_off = 0
gs 'gadukino_event', 'sound'
cls
gs'stat'
clothesAtLocation = FUNC('lost_clothes_here',$CURLOC)

if $ARGS[0] = '':
	act 'Go back to the hut':minut += 5 & gt'swamp_yard'

	'<center><B>Backwater</B></center>'
	
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwater.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwater_night.jpg"></center>'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwater_winter.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwater_winter_night.jpg"></center>'
		end
	end
	*nl
	
	'A small creek with relatively clean water. If the weather is warm you can clean yourself up. Also, if your clothes are dirty you can wash them here.'
	*nl
	if clothesAtLocation = 1:'Your clothes lie on the shore.'
	
	if $clothingworntype = 'nude':
		act 'Wash self':
			cla
			*clr
			menu_off = 1
			dynamic $showerdin
			face_write = 0
			body_write = 0
			minut += 30
			pcs_mood += 10
			pcs_willpwr += 5
			inhib_exp += rand(1,3)
			if huntersRape = 1: huntersRape = 2
	
			if $pantyworntype = 'none':
				'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwaterswim2.jpg"></center>'
			else
				'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwaterswim1.jpg"></center>'
			end
				*nl
			'You washed yourself in the creek.'
	
			act 'Finish':gt 'backwater'
		end
	end
	if cumloc[11] = 1 or cumloc[12] = 1:
		act 'Clean the sperm from your face (0:10)':
			cla
			*clr
			menu_off = 1
			pcs_makeup = 1
			cumspclnt = 9
			gs 'cum_cleanup'
			cumspclnt = 13
			gs 'cum_cleanup'
			if pcs_sweat > 19 : pcs_sweat -= 5
			minut += 10
			if huntersRape = 1: huntersRape = 2
			gs 'stat'
	
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwaterswim1.jpg"></center>'
			*nl
			'You carefully wash off the sperm in the creek.'
			act 'Finish':gt 'backwater'
		end
	end
	if body_write > 0 or face_write > 0:
		act 'Wash the writings off your body (0:10)':
			cla
			*clr
			menu_off = 1
			body_write = 0
			face_write = 0
			minut += 10
			gs'stat'
	
			'<center><img <<$set_imgh>> src="images/shared/home/bathroom/wash1.jpg"></center>'
			*nl
			'You wash off the indecent inscriptions on your body.'
	
			act 'Finish':menu_off = 0 & gt $loc, $metka
		end
	end
	if cumloc[6] = 1 or cumloc[7] = 1:
		act 'Wipe the cum off your clothes (0:10)':
			cla
			*clr
			menu_off = 1
			cumspclnt = 6
			gs 'cum_cleanup'
			minut += 10
			if huntersRape = 1: huntersRape = 2
			gs'stat'
	
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/cumwash.jpg"></center>'
			*nl
			'You clean the cum stains off your clothes.'
	
			act 'Finish':gt 'backwater'
		end
	end
	
	if spdirty > 0:
		act 'Quick wash (0:15)':
			cla
			*clr
			menu_off = 1
			cumspclnt = 4
			gs 'cum_cleanup'
			if pcs_sweat < 30:
				pcs_sweat = 15 + rand(0,4)
			else
				pcs_sweat -= 10 + rand(0,4)
			end
			minut += 15
			if huntersRape = 1: huntersRape = 2
			gs'stat'
			'<center><img <<$set_imgh>> src="images/shared/home/bathroom/facesp.jpg"></center>'
			*nl
			'You carefully wash your face, removing any impurities.'
			act 'Finish':gt 'backwater'
		end
	end
	
	if clothesAtLocation = 0:
		if $clothingworntype ! 'nude' and temper >= 15:
			act'Get undressed':
				menu_off = 1
				if huntersKnowSlut > 0 or hunters = 0 or hour < 8 or hour >= 20 or pcs_inhib > 35:
					cla
					*clr
					minut+=5
					gs 'underwear', 'remove'
					if huntersRape = 1: huntersRape = 2
	
					if CloSkirtShortness > 0:
						'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwaterundress_ski.jpg"></center>'
					else
						'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwaterundress.jpg"></center>'
					end
					gs 'clothing', 'strip', 'backwater'
					'You do not hesitate getting undressed, not caring if anyone is watching.'
					act'Back':gt'backwater'
				else
					cla
					*clr
					'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/noundress.jpg"></center>'
					'You hesitate getting undressed, what if someone suddenly sees you?'
					act'Back':gt'backwater'
				end
			end
		else
		'It''s too cold to undress. Maybe you should bathe inside the hut'
		end
	else
		if $clothingworntype ! 'nude':
			act 'Pick up your clothes':
				cla
				*clr
				menu_off = 1
				gs 'clothing', 'recover_lost_clothes', 'backwater'
				'You pick up your clothes laying on the ground.'
				gt $loc, $metka
			end
		elseif forced_naked_day < daystart:
			act 'Get dressed':
				cla
				*clr
				menu_off = 1
				gs 'clothing', 'recover_lost_clothes', 'backwater', 1
				gs 'underwear', 'wear'
				'You pick up your clothes laying on the ground and get dressed.'
				gt $loc, $metka
			end
		end
	end
	
end

if $ARGS[0] = 'creekside_relax':

end

if $clothingworntype = 'nude' and igor_away = 'no' and rand(1,10) = 1:
	cla
	*clr
	menu_off = 1
	minut += 5
	gs 'stat'
	'<center><B>Backwater</B></center>'
	'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/hunter_looks.jpg"></center>'
	'While cleaning yourself in the backwater, you suddenly get the feeling that you are not alone here. Turning around, you see one of the hunters. He''s just standing there watching you, his camouflage is almost indistinguishable compared to the bushes and trees.'

	if clothesAtLocation = 1:
		act'Get dressed':
			cls
			minut += 5
			gs 'underwear', 'wear'
			gs 'clothing', 'recover_lost_clothes', 'backwater', 1

			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swampbackwaternude1.jpg"></center>'
			'Noticing the hunter, you rush over to your clothes and begin dressing yourself in panic. As you get fully dressed, you look over your shoulder, expecting the hunter standing there, but there is no one there.'
			act'Further':gt 'backwater'
		end
	else
		act'Hide':
			cls
			minut += 1
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swampbackwaternude2.jpg"></center>'
			'Covering your breasts and crotch you jump into the nearest bush. In your state of fear and confusion, you start scanning the area but you don''t see anyone there. You quickly run back towards the cabin.'
			act'Go back to the hut':gt 'swamp_yard'
		end
	end
	act'Stay naked':
		cls
		hunterslut += 1
		hunterknowday = day
		gs'stat'
		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swampbackwaternude3.jpg"></center>'
		'You decide that you have nothing to be afraid of, and continue on completely nude. The hunter looks at you for a minute or so, all of a sudden you hear a "Hmph", him turning around and going away.'
		act'Further':gt 'backwater'
	end
	exit
end

if huntersRape = 2:
	cla
	*clr
	menu_off = 1
	minut += 5
	huntersRape = 3
	gs 'stat'

	'<center><img <<$set_imgh>> src="images/locations/gadukino/sex/hunter/backwaterrelax.jpg"></center>'
	'You sit down by the shore, resting.'

	act 'Further':gt 'hunters', 'start'
end

--- backwater ---------------------------------

