# bedrPar

CLOSE ALL
$loc = 'bedrPar'
$metka = ''
$location_type = 'private'
$locclass = 'bedr'
$menu_loc = 'bedrPar'
$menu_arg = ''
menu_off = 0
*clr & cla
minut += 1
gs 'stat'
gs 'themes', 'indoors'

if saveparQW = 2:
	cls
	'As soon as you enter the room you rush over to <<$namepopu2>>''s  cage and pour the contents of the vial in to his water bowl.'
	'You really hope this will work and keep him from spilling yours and your  sister''s bedroom secrets.'
	saveparQW = 3
	saveparjourQW = 3
	act 'Continue':gt $loc, $metka
	exit
end
if sisrelparday + 3 < daystart and saveparQW = 1:
	cls
	'When you enter the room you notice <<$namepopu2>>''s cage is open and he is gone.'
	'You sister must have released him since you were not able to find a solution in time.'
	popugai2 = 0
	saveparQW = 0
	saveparjourQW = 2
	saveparQWfailed = 0
	act 'Continue':gt $loc, $metka
	exit
end

if pcs_magik >= 6 and spellbefshild = 1 and tobiQW = 0 and sisterHere < 2:gt 'tobiQW', 'start'

if sisboyday + 1 = daystart and hour = 18:gt'sisterQW','sisboyQW_expel'

if sisterHere = 2 and popugai2 = 1 and sispopconf = 0:
	cls
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/anya/sister2.jpg"></center>'
	'"What is that ?" your sister asks while pointing at <<$namepopu2>> in his cage'
	'"I went to the shopping center and bought this bird. Isn''t he the cutest ?"'
	if npc_rel['A33'] > 50:
		'"Not really, but I can see this is something you really want. So as long as he doesn''t make too much noise I don''t really care"'
		sispopconf = 1
	end
	if npc_rel['A33']  <= 50:
		'"No he is not cute, and I suggest you take it out of here, I am not sharing my room with a bird. "'
		'You feel kind of sad but you understand your sister. You should have asked her before buying it.'
		'You go to the window, open it and release the bird'
		popugai2 = 0
	end

	act 'Leave':gt $loc, $metka
	exit
end

'<center><h1><font color="maroon">Your bedroom</font></h1></center>'
! WD: Correct Image path ~ "images/qwest/alter/<<$loc>>.jpg"
'<center><img <<$set_imgh>> src="images/locations/pavlovsk/resident/apartment/home/bedrpar.jpg"></center>'
*nl
'This is the small bedroom you share with your sister, Anya. On one side of the room is a <a href="exec:gt ''wardrobe'', ''start''">wardrobe</a> where you can choose outfits and organise your clothing.'
*nl
'Your <a href="exec:gt ''bed'', ''start''">bed</a> stands opposite the wardrobe, and your <a href="exec:gt ''stol'' ,''stolPar''">desk</a> is next to it. Your sister''s bed is in the other corner.'
*nl
if popugai2 = 1:
	'<a href="exec:GT ''popu'',''start2''"><<$namepopu2>></a> is sitting in his cage'
	if daystart - parbuydate <= 10: '<<$namepopu2>> starts squawking at you when you enter the room.'
	if daystart - parbuydate > 10 and daystart - parbuydate <= 20: 'When <<$namepopu2>> sees you he starts ''speaking''. It sounds almost like he is saying "<b>hello  <<$pcs_nickname>></b>"'
	if daystart - parbuydate > 20 and daystart - parbuydate <= 30: 'When <<$namepopu2>> sees you he starts ''speaking''. He is clearly saying "<b>hello  <<$pcs_nickname>></b>"'
	if daystart - parbuydate > 30 and daystart - parbuydate <= 35: 'When <<$namepopu2>> sees you he starts ''speaking''. He is saying "<b><<$pcs_nickname>> very pretty</b>"'
	if daystart - parbuydate > 35 and daystart - parbuydate <= 40: 'When <<$namepopu2>> sees you he starts ''speaking''. He is saying "<b><<$pcs_nickname>> great body</b>"'
	if daystart - parbuydate > 40 and daystart - parbuydate <= 50: 'When <<$namepopu2>> sees you he starts ''speaking''. He is saying "<b><<$npc_firstname[''A33'']>> you''re the best</b>"'
	if daystart - parbuydate > 50 and daystart - parbuydate <= 55: 'When <<$namepopu2>> sees you he starts ''speaking''. He is saying "<b>Yeah <<$npc_firstname[''A33'']>> swallow it all</b>"'
	if daystart - parbuydate > 55 and saveparQW < 3: 'When <<$namepopu2>> sees you he starts ''speaking''. He is saying "<b>Yeah <<$npc_firstname[''A33'']>>  your pussy tastes great</b>"'
	if  saveparQW = 3:
		parfixrand = rand(0,9)
		if parfixrand = 0:'When <<$namepopu2>> sees you he starts ''speaking''. He  is saying "<b>What Did You Say To Me?</b>"'
		if parfixrand = 1:'When <<$namepopu2>> sees you he starts ''speaking''. He  is saying "<b>3.1415926535897932384 ....</b>"'
		if parfixrand = 2:'When <<$namepopu2>> sees you he starts ''speaking''. He  is saying "<b>Come Closer, My Friend</b>"'
		if parfixrand = 3:'When <<$namepopu2>> sees you he starts ''speaking''. He  is saying "<b>Here Kitty, Kitty!</b>"'
		if parfixrand = 4:'When <<$namepopu2>> sees you he starts ''speaking''. He  is saying "<b>I Just Met You and This is Crazy</b>"'
		if parfixrand = 5:'When <<$namepopu2>> sees you he starts ''speaking''. He  is saying "<b>Bow Before Me!</b>"'
		if parfixrand = 6:'When <<$namepopu2>> sees you he starts ''speaking''. He  is saying "<b>He’s Just a Friend, Honest!</b>"'
		if parfixrand = 7:'When <<$namepopu2>> sees you he starts ''speaking''. He  is saying "<b>What Did You Do To My Socks?</b>"'
		if parfixrand = 8:'When <<$namepopu2>> sees you he starts ''speaking''. He  is saying "<b>That’s What She Said!</b>"'
		if parfixrand = 9:'When <<$namepopu2>> sees you he starts ''speaking''. He  is saying "<b>Help! They’ve Turned Me Into a Parrot!</b>"'
	end

	*nl
end
if budilnikOn = 0:
	'Your <a href="exec:gt ''budilnik'', ''start''">alarm clock</a> is set to '+iif(timer < 10,'0<<timer>>','<<timer>>')+':'+iif(timerM < 10,'0<<timerM>>','<<timerM>>')+' and is currently turned <a href="exec: budilnikOn = 1 & GT ''<<$loc>>''">off</a>.'
else
	'Your <a href="exec:gt ''budilnik'', ''start''">alarm clock</a> is set to '+iif(timer < 10,'0<<timer>>','<<timer>>')+':'+iif(timerM < 10,'0<<timerM>>','<<timerM>>')+' and is currently turned <a href="exec: budilnikOn = 0 & GT ''<<$loc>>''">on</a>.'
end

if tabletki > 0 and prezik + prezikcount + preziksabo > 0:
	'Your <a href="exec:gt ''stol'', ''bc''">birth control</a> is hidden in one of your desk drawers, as are your <a href="exec:gt ''stol'', ''bc''">condoms</a>.'
elseif tabletki > 0:
	'Your <a href="exec:gt ''stol'', ''bc''">birth control</a> is hidden in one of your desk drawers.'
elseif prezik + prezikcount + preziksabo > 0:
	'Your <a href="exec:gt ''stol'', ''bc''">condoms</a> are hidden in one of your desk drawers.'
end

if sewing_kit = 1:
	'Your <a href="exec:gt ''sewing'',''start''">sewing kit</a> is stored neatly under your bed.'
	*nl
	'You have <<tkan>> pieces of sewing fabric left.'
end

if komp = 1:'Your <a href="exec:gorodokKomp = 1 & GT ''Komp'',''start''">laptop</a> is on your desk.'
gorodokKomp = 0

gad = 0
gs 'subkid'
if npc_QW['A33'] > 10:npc_QW['A33'] = 10

gs'dinsister'

act 'Exit the room':
	if sisterHere = 2 and daystart - parbuydate > 55 and popugai2 = 1 and sisrelparday = 0 :gt'sister','pargone'
	gt'korrPar'
end
if sisboyparty_day + 1 = daystart and sisboyparty = -1 and hour = 22:
	gs'family'
else
	gs'family'

	if rexCarCount >= 9 and sisterHere = 1 and hour < 3 and rexSmsDay ! daystart and smsForRex < 13:
		act 'Send Rex a SMS message':gt 'reks','sms'
	end

	if student > 0 and kursovik < 100:
		act 'Do coursework (1:00)':
			cla
			*clr
			minut += 60
			kursovik += rand(pcs_intel / 20, pcs_intel / 10)

			if kursovik > 100:kursovik = 100

			pcs_mood -= 10
			'<center><img <<$set_imgh>> src="images/pc/activities/study.jpg"></center>'
			'You sit down at the desk and do your coursework, you''ve finished <<kursovik>> percent of it.'

			act 'Leave your desk':gt $curloc
		end
	end

	!!Masturbation, allowed after sister threesome (when sis_watch is used for a message) or sister not here.
	sis_watch = 0

	if pcs_horny >= 50 and (sisboytrio > 0 or sisterHere = 0):
		act 'Masturbate':gt 'selfplay', 'start'
	end

	if pornMagazine > 0:'You have hidden your porn magazines under the mattress of your bed.'
	if (sisboytrio > 0 or sisterHere = 0) and pornMagazine > 0:gs 'events', 'read_porn'

	gs 'events', 'read'

	if tovarL > 0:'You currently have <<tovarL>> trinkets to sell at the station. The maximum you can carry with you is 30.'


	if vcard > 0:
		act 'View your vintage porn cards':card_deck = 0 & gt 'card'
	end
	if card > 0:
		act 'View your new porn cards':card_deck = 1 & gt 'card'
	end

	if dur > 0:'You have hidden <<dur>> doses of Pale Lady.'

	if dur > 0 and narkday ! daystart:
		act 'Use some Pale Lady (0:05)':
			cla
			*clr
			minut += 5
			narkday = daystart
			pcs_health = pcs_vital * 10 + pcs_stren * 5
			pcs_willpwr = pcs_intel * 5 + pcs_sprt * 5
			pcs_mood = 100
			pcs_horny = 100
			nark += 10
			dur -= 1
			'<center><img <<$set_imgh>> src="images/shared/drugs/dur.jpg"></center>'
			'You snort some Pale Lady, feeling dizzy for just a moment. After that you feel fantastic, and incredibly horny.'

			act 'Continue':gt $curloc
		end
	end
end

gs 'exercise', 'start'

if gobelen>0:'You have <<gobelen>> completed tapestries.'
if pcs_sewng >= 40 and tkan > 0:'You have enough fabric for <<tkan>> lessons.'
if pcs_sewng >= 40 and tkan <= 0:'You do not have any fabric.'
if pcs_sewng >= 80:
	if newgobelen = 0 and tkan > 0:
		act'Start a new tapestry':
			cls
			tkan -= 1
			newgobelen = 1
			minut += 15
			gs'stat'
			'You spend 15 minutes at the embroidery frame, preparing the fabric and outlining the pattern for the tapestry.'
			act'Set the tapestry aside':gt $curloc
		end
	end
	if newgobelen>=1:
		'Your tapestry is <<newgobelen/10>> percent finished.'
		act'Work on the tapestry for an hour':
			cls
			minut+=60
			sewng_exp += rand(pcs_intel/10, pcs_intel/5)
			gobramax = (pcs_sewng - 70) * 6
			gobramin = (pcs_sewng - 70) * 2
			newgobelen += rand(gobramin,gobramax)
			gs'stat'
			if newgobelen < 1000:'You spent an hour working on your tapestry, which is now <<newgobelen/10>> percent finished.'
				if newgobelen >= 1000:
				newgobelen = 0
				gobelen += 1
				'Your work on the tapestry is done.'
			end
			act'Set the tapestry aside':gt $curloc
		end
	end
end

if sisterHere = 0 and breastpump = 1 and bp_unbox <= 0:
	'<a href="exec: gt ''lact_lib'', ''bp_unbox_event''">Unbox</a> your breast pump'
elseif kid >= 1 and breastpump = 1 and bp_unbox <= 0:
	'<a href="exec: gt ''lact_lib'', ''bp_unbox_event''">Unbox</a> your breast pump'
elseif sisterHere = 0 and breastpump = 1 and bp_unbox = 1 and pcs_inbition > 40:
	'<a href="exec: gt ''lact_lib'', ''bp_unbox_event''">Try out</a> your breast pump'
elseif kid >= 1 and breastpump = 1 and bp_unbox = 1 and pcs_inbition > 30:
	'<a href="exec: gt ''lact_lib'', ''bp_unbox_event''">Try out</a> your breast pump'
end
gs 'lact_lib', 'massage'


act 'Relax on your bed': gt 'bed', 'start'


if $ARGS[0] = 'intro':
	cla & *clr
	$metka = ''
	$loc = 'korrPar'
	$temp_nickname = '<<$npc_nickname[''A29'']>>'
	monthbabkapay = month
	menu_off = 1
	hour = 6
	gs 'stat'
	'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost4.jpg"></center>'
	*nl
	'All of a sudden you hear a familiar voice yell out, "<<$pcs_nickname>>, time to get up. You can''t spend the whole day laying around!"'
	wait 2000
	$npc_nickname['A29'] = input('<center>What name do you address your mother by?<br>Mother (default), Mum, Mom, Mama, etc.</center>')
	if $npc_nickname['A29'] = '':$npc_nickname['A29'] = '<<$temp_nickname>>'
	killvar '$temp_nickname'

	act 'Continue':
		cla & *clr
		minut += 2
		gs 'stat'
		'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
		'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost5.jpg"></center>'
		*nl
		'You tiredly open your eyes seeing your <<$npc_nickname[''A29'']>> standing over you, "Fifteen more minutes..."'
		'Your <<$npc_nickname[''A29'']>> lets out a loud sigh, "No, up now!" as she yanks the cover off...'
		'"<<$npc_nickname[''A29'']>>!" you exclaim, as you feel a slight cold breeze hit your body.'
		'"No excuses, <<$pcs_nickname>>. The summer holidays will be over soon and you need to get used to waking up early, for when school starts. So, from now on set your alarm, so you can wake up at a reasonable time." <<$npc_nickname[''A29'']>> says as she stares at you expectantly.'
		act 'Get up':
			cla & *clr
			minut += 2
			gs 'stat'
			'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
			'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost4.jpg"></center>'
			*nl
			'As soon as you get off your now coverless bed, your <<$npc_nickname[''A29'']>> is quick to resume her nagging, telling you again what you already heard from her a few moments ago. "Summer is almost over, you can''t sleep in anymore, not even for another fifteen minutes." She''s not even looking at you while she talks, but luckily for you that means she misses you rolling your eyes.'
			'"What are you going to do when school starts, or when you get a job? You better get into the habit of setting your alarm so you can wake up bright and early."'
			act 'Continue':
				cla & *clr
				minut += 2
				gs 'stat'
				'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
				'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost5.jpg"></center>'
				*nl
				'You let out an exasperated sigh, "Yeah, yeah I know, you told me that already. So, how early are we talking?"'
				'Your <<$npc_nickname[''A29'']>> shoots you a sharp look and you know to fix your attitude or else. "Well, depends on what you plan to do in the morning. There''s showering and brushing your teeth, doing your hair and then makeup and shaving if you really need to. If I remember right...Anya used to wake up at 06:00 for school."'
				act 'Continue':
					cla & *clr
					minut += 2
					gs 'stat'
					'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
					'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost4.jpg"></center>'
					*nl
					'"You want me to wake up that early?!" You huff. "Can''t I wake up at like... 07:00?"'
					'Your <<$npc_nickname[''A29'']>> shrugs. "You could, but then you might have to cut corners or even end up running late to school, and I won''t be having any of that. Your teachers won''t either," she warns you before making her way towards the kitchen. "Oh, don''t forget; you need to head down to the clinic and see the gynecologist for a check up. Not just once, but every month," she adds quickly.'
					act 'But why do I--':
						cla & *clr
						minut += 2
						gs 'stat'
						'<center><h1><font color="maroon">Your Bedroom</font></h1></center>'
						'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/mom/prost5.jpg"></center>'
						*nl
						'Before you can finish, she cuts you off. "I''m not going to have you running around town like so many of those other girls I see! Opening their legs for any boy that comes around..." She shakes her head in disgust. "I want a referral every month and it better tell me you''re still a virgin or else, understand?" <<$npc_nickname[''A29'']>> says as she storms out of the room, without giving you a chance to respond..'

						act 'I understand...':gt 'budilnik', 'start'
					end
				end
			end
		end
	end
end

--- bedrPar ---------------------------------

