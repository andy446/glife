# downlibrary

$metka = $ARGS[0]
$loc = $CURLOC
$menu_loc = 'downlibrary'
$menu_arg = ''
menu_off = 0
$location_type = 'public_indoors'
frost = 0
gs 'stat'
gs 'themes', 'indoors'

! Stuff to do on Studying a Book
!	$ARGS[0] = Skill to Check
$downlibrary['BookStudy'] = {
	$downlibrary['Skill'] = $ARGS[0]
	*clr & cla
	menu_off = 1
	if blizoruk = 100 or glassqw = 1:
		glassqw = 1
		msg'	The text blurs across the page, it seems you have poor eyesight. Maybe you should visit an ophthalmologists.'&gt $curloc
	end
	if pcs_nerd > 0:
		lastread = totminut
		lastreadday = daystart
		pcs_nerd += 1
	end
	minut += 60
	if $downlibrary['Skill'] ! '':
		dynamic "
			if pcs_<<$downlibrary['Skill']>> >= 50:
				<<$downlibrary['Skill']>>_exp += RAND(0,1)
			elseif pcs_<<$downlibrary['Skill']>> >= 25:
				<<$downlibrary['Skill']>>_exp += RAND(1,2)
			else
				<<$downlibrary['Skill']>>_exp += RAND(1,3)
			end"
	end

	blizoruk += 1
	gs'stat'
}

'<center><b><font color="maroon">National Library of Russia</font></b></center>'
'<center><img <<$set_imgh>> src="images/locations/city/citycenter/library/downlibint.jpg"></center>'
'A huge library with thousands upon thousands of books. You could spend years here and you probably still wouldn''t be able to read it all.'

act 'Leave the library': minut += 5 & gt 'down'

if hour >= 8 and hour <= 18:
	act 'Read a science book (1:00)':
		intel_exp += 4 + (mentats_dose - rand(0,mentats_dose))

		dynamic $downlibrary['BookStudy'], ''

		'	You read the book for an hour trying to make sense of all clever propositions and understand the subject of the description.'

		act 'Put the book down':gt $curloc
	end

	act 'Read an entertaining book (1:00)':
		pcs_mood = 100
		pcs_willpwr += 100

		dynamic $downlibrary['BookStudy'], ''

		'	You read an adventure fiction for an hour.'

		act 'Put the book down':gt $curloc
	end

	act 'Read the collected works of Leo Tolstoy (1:00)':
			dynamic $downlibrary['BookStudy'], 'sprt'

		'	You read the book for an hour. Tolstoy is certainly a classic writer, no one denies this, but he wrote exceptionally wordily.'

		act 'Put the book down':gt $curloc
	end
	if MagicLibrary = 1:
		act 'Read in magic books':
			dynamic $downlibrary['BookStudy'], 'splcstng'

			'	You pick out one of the magic books you can actually read and sit down with it.'
			'Not only is the subject matter complex and mostly a mystery to you, but the descriptions and explanations are unnecessarily wordy, as if the writer had something to prove.'
			'After about an hour, you decide to take a break from it and, instead, test your memory. You don''t remember much, but you understand magic a tiny bit better than before nonetheless.'

			! Find Researchable Spells
			if arrsize('$ResearchingSP') < arrsize('$librarySpells'):
				if rand(1,10) = 10:
					NumResearching = arrsize('$ResearchingSP') + 1
					if spellKnown[$librarySpells[NumResearching]] = 0:
						*nl
						*pl 'You have come across a lead on a new spell.'
					end
					i=0
					:FindNewSpell1
					if i < NumResearching:
						$ResearchingSP[i] = $librarySpells[i]
						i += 1
						jump 'FindNewSpell1'
					end
					killvar 'i'
					killvar 'NumResearching'
				end
			end

			if arrsize('$ResearchingSP') > 0:
				gs 'spellList', 'teacherActions', '$ResearchingSP', 'downlibrary', ''
			end
			act 'Put the book down':gt $curloc
		end
	end

else
	'The library is closed.'
end



--- downlibrary ---------------------------------

