# drugs

!! How to use:
!! Smoking: gs 'drugs', 'smoke' (if it isn''t svetas cigarette, you have to add siga += 1)
!! Joint/Weed: gs 'drugs', 'joint' (if it isn''t svetas joint, you have to add joint += 1)
!! Heroin: gs 'drugs', 'heroin'
!! Cocaine: gs 'drugs', 'cocaine' (if it isn''t svetas cocaine, you have to add dur += 1)

!! Alcohol: gs 'drugs', 'alcohol', 'type', count
!! Types: beer, champagne, moonshine, rum, scotch, vodka, wine, whiskey
!! Count: Number of drinks of the chosen type sveta drinks (if the number is 1, you don''t have add the count, but it isn''t a problem if you do)
!! Example 1: sveta drinks vodka gs 'drugs', 'alcohol', 'vodka' (or gs 'drugs', 'alcohol', 'vodka', 1)
!! Example 2: sveta drinks 2 beer gs 'drugs', 'alcohol', 'beer', 2

if $ARGS[0] = 'smoke':
	if NarkImmune = 0: smoker += 1
	siga -= 1
	smokeDay = daystart
	smokeHour = hour
	smokeminut = minut + 5
	smokerNeed = 0
	cumspclnt = 2
	gs 'cum_cleanup'
	pcs_breath = 0
	pcs_mood += 100
	wipo += 100
	if pcs_hydra >= 100:
		pcs_hydra -= 5
	else
		pcs_hydra -= 10
	end
	if pcs_energy < 80: pcs_energy += 4
	gs '$menu_obnovit'
elseif $ARGS[0] = 'joint':
	if NarkImmune = 0: joint_smoked += 1
	joint -= 1
	jointday = daystart
	jointhour = hour
	jointminut = minut + 5
	if minut > 30:
		jointhigh = 2
	else
		jointhigh = 1
	end	
	cumspclnt = 2
	gs 'cum_cleanup'
	pcs_breath = 0
	pcs_mood += 100
	if pcs_hydra >= 100:
		pcs_hydra -= 5
	else
		pcs_hydra -= 10
	end
	if pcs_energy > 20: 
		pcs_energy -= 20
	else
		pcs_energy = 0
	end
	gs '$menu_obnovit'
elseif $ARGS[0] = 'heroin':
	if NarkImmune = 0: SNarkTimes += 1
	StrongNarkota += 25
	pcs_mood = 100
	pcs_health -= 10
	gs '$menu_obnovit'
elseif $ARGS[0] = 'cocaine':
	dur -= 1
	pcs_health = pcs_vital * 10 + pcs_stren * 5
	pcs_willpwr = pcs_intel * 5 + pcs_sprt * 5
	pcs_mood = 100
	pcs_horny = 100
	nark += 10
elseif $ARGS[0] = 'alcohol':
	if ARGS[2] <= 1: 
		alc_count = 1
	else
		alc_count = ARGS[2]
	end
	:alcohol_jump
	alc_count -= 1
	frost = 0
	pcs_health -= 5
	pcs_energy -= 5
	cumspclnt = 2
	gs 'cum_cleanup'
	pcs_breath = 0
	if $ARGS[1] = 'beer':
		alko += 1
		fat += 3
		pcs_mood += 10
		if pcs_hydra >= 100:
			pcs_hydra += 5
		else
			pcs_hydra += 10
		end
	elseif $ARGS[1] = 'wine' or $ARGS[1] = 'champagne':
		alko += 2
		fat += 4
		pcs_mood += 20
		if pcs_hydra >= 100:
			pcs_hydra += 5
		else
			pcs_hydra += 10
		end
	elseif $ARGS[1] = 'vodka' or $ARGS[1] = 'whiskey' or $ARGS[1] = 'scotch' or $ARGS[1] = 'rum':
		alko += 3
		fat += 6
		if pcs_hydra >= 100:
			pcs_hydra -= 5
		else
			pcs_hydra -= 10
		end
	elseif $ARGS[1] = 'moonshine':
		alko += 5
		fat += 6
		if pcs_hydra >= 100:
			pcs_hydra -= 5
		else
			pcs_hydra -= 10
		end
	else
		'<br><font color = red>Developer note: The type of alcohol is missing in this scene. Please report this and the following variable: $curloc = <<$curloc>> as a bug.</font>'
	end
	if alc_count > 0: jump 'alcohol_jump'
	killvar 'alc_count'
else
	'<br><font color = red>Developer note: The type of drug is wrong or missing in this scene. Please report this and the following variable: $curloc = <<$curloc>> as a bug.</font>'
end

--- drugs ---------------------------------

