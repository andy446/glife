# gaddvor

if $ARGS[0]='':
	music_loop = 0
	$metka = ''
	$loc = 'gaddvor'
	$location_type = 'public_outdoors'
	$menu_loc = 'gaddvor'
	$menu_arg = ''
	menu_off = 0
	killvar '$locclass'
	gs 'stat'
	CLOSE ALL
	gs 'gadukino_event', 'sound'

	if StoryLine = 1:
		'<center><h4>Your grandparents'' yard</h4></center>'
	else
		'<center><h4>Old couple''s yard</h4></center>'
	end
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/dvor.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/dvor_night.jpg"></center>'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/dvor_winter.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/dvor_winter_night.jpg"></center>'
		end
	end
	*nl
	'A fenced yard behind your grandparents'' house. The yard contains a small barn, garden, root cellar, and banya where you can bathe.'

	if $clothingworntype = 'nude':
		*nl
		'You are naked and can only go into your grandparent''s house to get dressed or go to the banya.'
		act 'Enter your grandparents'' house':minut += 1 & gt 'gadhouse', 'start'
		act 'Enter the banya':minut += 1 & gt 'gadbana'
	else
		if grandmaQW['chore_feed_chickens'] = 1 or (hour >= 6 and hour < 20 and month >= 4 and month <= 10):'In the yard there are several <a href="exec:gt ''gaddvor'',''chickens''">chickens</a> running around.'

		if mira_guest = 1: Miraloc = 7 & 'Your friend <a href="exec: gt ''miroslava'', ''Start''">Mira</a> stands next to you.'
		if MiraLoc ! 7: mira_guest = 0

		act '<b>Leave and go into the village</b>':minut += 5 & gt 'gadukino'
		if 	grandpaQW['chore_clean_yard'] = 1:
			act 'Clean up the yard for Grandpa (1:00)':
				*clr & cla
				if month >= 4 and month <= 10:
					'<center><img <<$set_imgh>> src="images/locations/gadukino/village/clean_yard1.jpg"></center>'
					'Armed with a rake, you begin cleaning the yard. You''re pleased seeing the yard clean. You return the rake to the shed and continue on with your business.'
				else
					'<center><img <<$set_imgh>> src="images/locations/gadukino/village/clean_yard2.jpg"></center>'
					'Armed with a shovel, you begin clearing the yard of snow. You''re pleased with yourself seeing the courtyard is free of snow. You return the shovel to the shed and continue on with your business.'
				end
				minut += 60
				grandpaQW['chore_clean_yard'] = 2
				if month >= 4 and month <= 10:
					fat -= rand(1,3)
					pcs_stam -= 5
					gs 'sweat', 'add', 10
				else
					stren_exp += 1
					fat -= rand(1,5)
					pcs_stam -= 10
					gs 'sweat', 'add', 5
				end
				gs'stat'

				act 'Finish':gt'gaddvor'
				act 'Return back to your grandfather': minut += 1 & gt 'gadhouse', 'grandpa'
			end
		end
		act 'Enter your grandparents'' house':minut += 1 & gt 'gadhouse', 'start'
		act 'Enter the banya':minut += 1 & gt 'gadbana'
		act 'Go to the barn':minut += 1 & gt 'gadsarai'
		act 'Go to the root cellar': minut += 1 & gt 'gaddvor', 'root_cellar'
		act 'Go to the garden':minut += 1 & gt'gadgarden','garden'
		if grandmaQW['chore_feed_chickens'] = 1 or (hour >= 6 and hour < 20 and month >= 4 and month <= 10):act 'Look at your grandparents'' chickens':gt 'gaddvor','chickens'
		mira_temp = rand(1,5)
		if mira_temp = 3 and hour >= 9 and hour <= 19 and npc_rel['A60'] >= 15 and Mira_no = 0 and sunWeather = 1 and mira_guestday ! daystart and (npc_QW['A63'] < 13 or npc_QW['A60'] >= 10 or mirasex > 1):
			*clr & cla
			menu_off = 1
			gs 'miroslava', 'miraclothes'
			'You went out into the yard and noticed Mira standing there.'
			'"Oh, <<$pcs_nickname>> hi, hope you don’’t mind me visiting. I was bored being home alone." she said smiling.'
			mira_guestday = daystart
			minut += 5
			mira_guest = 1
			gs 'stat'

			act 'Continue':gt'gaddvor'
		end
	end

	clothesAtLocation = FUNC('lost_clothes_here',$CURLOC)
	if clothesAtLocation = 1:
		if $clothingworntype = 'nude':
			act 'Get dressed':
				gs 'clothing', 'recover_lost_clothes', 'gaddvor', 1
				gs 'underwear', 'wear'
				gt 'gaddvor'
			end
		else
			act 'Pick up your clothes':
				gs 'clothing', 'recover_lost_clothes', 'gaddvor'
				'You picked up your clothes.'
				gt 'gaddvor'
			end
		end
	end

end

if $ARGS[0] = 'root_cellar':
	*clr & cla
	'<center><h4>Root Cellar</h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/gadukino/village/root_cellar.jpg"></center>'
	*nl
	if boletus_stored + bilberry_stored >= 20:
		'Your grandparents'' root cellar is full.'
	elseif boletus_stored + bilberry_stored > 0:
		'Your grandparents'' root cellar is partially full.'
	else
		'Your grandparents'' root cellar is empty.'
	end
	'Even though your grandparents allow you to store extra mushrooms and berries you pick in here, they reserve the right to use them too, so you aren''t surprised if any go missing day to day.'
	if boletus_stored > 0: 'You currently are storing <b><<boletus_stored>></b> kg of raw mushrooms.'
	if bilberry_stored > 0: 'You currently are storing <b><<bilberry_stored>></b> kg of raw berries.'
	*nl
	if boletus + boletus_cooked + bilberry >= 5:
		'Your basket is full and you cannot carry more.'
	elseif boletus + boletus_cooked + bilberry > 0:
		'Your basket is partially full.'
	else
		'Your basket is empty.'
	end
	if boletus > 0: 'You currently are carrying <b><<boletus>></b> kg of raw mushrooms.'
	if boletus_cooked > 0: 'You currently are carrying <b><<boletus_cooked>></b> kg of cooked mushrooms.'
	if bilberry > 0: 'You currently are carrying <b><<bilberry>></b> kg of raw berries.'

	act 'Go back to the yard': minut += 1 & gt 'gaddvor'
	if (boletus_stored + bilberry_stored < 20) and (boletus > 0 or bilberry > 0):
		act 'Store as many raw mushrooms and berries that will fit (Max 20 kgs):':
			:store_bb_loop
			if boletus > 0:
				boletus_stored += 1
				boletus -= 1
				if boletus_stored + bilberry_stored < 20: jump 'store_bb_loop'
			end
			if bilberry > 0:
				bilberry_stored += 1
				bilberry -= 1
				if boletus_stored + bilberry_stored < 20: jump 'store_bb_loop'
			end
			gt 'gaddvor', 'root_cellar'
		end
		if boletus > 0:
			act 'Store 1 kg of raw mushrooms':
				boletus_stored += 1
				boletus -= 1
				gt 'gaddvor', 'root_cellar'
			end
		end
		if bilberry > 0:
			act 'Store 1 kg of raw berries':
				bilberry_stored += 1
				bilberry -= 1
				gt 'gaddvor', 'root_cellar'
			end
		end
	end
	if (boletus_stored > 0 or bilberry_stored > 0) and (boletus + boletus_cooked + bilberry < 5):
		act 'Retrieve as many raw mushrooms and berries as you can carry (Max 5 kgs)':
			:retrieve_bb_loop
			if boletus_stored > 0:
				boletus += 1
				boletus_stored -= 1
				if boletus + boletus_cooked + bilberry < 5: jump 'retrieve_bb_loop'
			end
			if bilberry_stored > 0:
				bilberry += 1
				bilberry_stored -= 1
				if boletus + boletus_cooked + bilberry < 5: jump 'retrieve_bb_loop'
			end
			gt 'gaddvor', 'root_cellar'
		end
		if (boletus_stored > 0 and grandmaQW['chore_can_mushrooms'] ! 1) or (boletus_stored > 5 and grandmaQW['chore_can_mushrooms'] = 1):
			act 'Retrieve 1 kg of raw mushrooms':
				boletus += 1
				boletus_stored -= 1
				gt 'gaddvor', 'root_cellar'
			end
		end
		if (bilberry_stored > 0 and grandmaQW['chore_can_berries'] ! 1) or (bilberry_stored > 5 and grandmaQW['chore_can_berries'] = 1):
			act 'Retrieve 1 kg of raw berries':
				bilberry += 1
				bilberry_stored -= 1
				gt 'gaddvor', 'root_cellar'
			end
		end
	end
end

if $ARGS[0] = 'chickens':
	*clr & cla
	'<center><img <<$set_imgh>> src="images/locations/gadukino/village/chickens1.jpg"></center>'
	'Ordinary chickens clucking about their pen.'
	minut += 1
	gs 'stat'

	!job Granny
	if grandmaQW['chore_feed_chickens'] = 1:
		act 'Feed the chickens':
			*clr & cla
			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/chickens2.jpg"></center>'
			'You decide to feed the chickens. You pick up a bucket full of bird''s seed and grain mix and toss it on the ground. They immediately rush over and start to greedily eat. You watch them for a bit before you go about your business.'
			minut += 30
			grandmaQW['chore_feed_chickens'] = 2
			hndiwrk_exp += rand(0,3)
			gs 'stat'

			act 'Finish':gt'gaddvor'
			act 'Return back to your grandmother': minut += 1 & gt 'gadhouse', 'grandma'
		end
	end
	act 'Leave': gt'gaddvor'
end

--- gaddvor ---------------------------------

