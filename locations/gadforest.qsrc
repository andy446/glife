# gadforest

!MiraLoc tells us where Mira is Located
!----------------------------------------------
!MiraLoc = 0 (Mira at home)
!MiraLoc = 1 (Mira at river)
!MiraLoc = 2 (Mira at meadow)
!MiraLoc = 3 (Mira at forest)
!MiraLoc = 4 (Mira at field)
!MiraLoc = 5 (Mira with gop)
!---------------------------------------------

if $ARGS[0] = '1':
	*clr & cla
	! figure out if Sveta came from Mira code or not. events play out differently whether with Mira or not
	PC_arrive = iif($loc ! 'Miroslava', 1 , 0)

	! edge of forest
	$loc = 'gadforest'
	$metka = '1'
	$location_type = 'secluded'

	$menu_loc = 'gadforest'
	$menu_arg = '1'
	menu_off = 0

	clothesAtLocation = FUNC('lost_clothes_here',$CURLOC)
	gs 'gadukino_event', 'sound'
	gs'stat'

	*clr & cla

	! Add to knowledge of forest
	if goforest < 20:goforest += rand(0,1)
	! chance to get lost
	lost = 1
	lostrand = rand(1,20)
	! Determine where Sveta got lost in the forest
	foresteventrand = rand(1,20)
	new_boletus = 0
	new_bilberry = 0

	'<center><H4>Edge of the Forest</H4></center>'
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest1.jpg"></center>'
			'You are at the edge of the forest. Mushrooms and berries grow here and you can often find other people picking them.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_night1.jpg"></center>'
			'You are at the edge of the forest. Night in the woods is scary and uncomfortable.'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter1.jpg"></center>'
			'You are at the edge of the forest. The forest in winter is very beautiful.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter_night1.jpg"></center>'
			'You are at the edge of the forest. Night in the forest during wintertime is very scary and uncomfortable.'
		end
	end

	if forestpicnic = 1:'On the edge of the forest you see people. Apparently they are having a family <a href="exec:gt ''gadforest_event'',''picnic''">picnic</a>.'
	if MiraLoc = 3:'Nearby is <a href="exec: gt ''Miroslava'' ">Mira</a>.'
	if clothesAtLocation = 1:'Your clothes are nearby on the ground.'
	if findvel = 2:'Leaning up against a tree is your old <a href="exec:gt ''bicycle'',''forest''">bike</a>.'
	if goforest < 10:'You don''t know this part of the forest very well, it''s best if you don''t go any deeper into the woods.'

	! Sveta arrive without Mira events
	if PC_arrive = 1:
		if temper >= 15 and sunWeather = 1 and hour>=6 and hour<=20 and miratalkforest = 1:gt 'gadforest_event', 'nude_event'
		if foresteventrand < 3 and month >= 6 and month <= 9 and mushroom_pickersday ! daystart:mushroom_pickers = 1 & gt'gadforest_event', 'mushroom_pickers1'
		if foresteventrand = 3 and month >= 5 and month <= 9 and temper >= 15 and sunWeather = 1 and week > 5 and forestpicnic = 0: forestpicnic = 1 & gt 'gadforest_event', 'picnic'
		if foresteventrand = 4 and month >= 6 and month <= 9: gt 'gadforest_event', 'mushroom_pickers2'
		if foresteventrand = 5 and hour >= 8 and hour <= 15 and month >= 4 and month <= 10 and gadboyday ! daystart and week <= 5: gt 'gadforest_event', 'gopforest_mitka'
		if foresteventrand = 6 and hour >= 8 and hour <= 18 and month >= 4 and month <= 10 and gadboyday ! daystart and week <= 5: gt 'gadforest_event', 'gopforest_kolyamba'
		if foresteventrand = 7 and hour >= 8 and hour <= 18 and month >= 4 and month <= 10 and gadboyday ! daystart and week <= 5: gt 'gadforest_event', 'gopforest_vasyan'
		if foresteventrand = 8 and hour >= 8 and hour <= 15 and month >= 4 and month <= 10 and gadboyday ! daystart and week > 5: gt 'gadforest_event', 'gopforest_2boys'
		if foresteventrand = 9 and hour >= 8 and hour <= 15 and month >= 4 and month <= 10 and gadboyday ! daystart and week > 5: gt 'gadforest_event', 'gopforest_3boys'
	end

	act 'Leave the woods':
		if $clothingworntype = 'nude':
			*clr & cla
			inhib_exp += rand(0,2)
			if hour >= 6 and hour < 22:
				if rand(0,100) > 50:
					'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadukino_nude.jpg"></center>'
				else
					'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadukino_back.jpg"></center>'
				end
			else
				if rand(0,100) > 50:
					'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadukino_nude.jpg"></center>'
				else
					'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadukino_back_night.jpg"></center>'
				end
			end
			if clothesAtLocation = 0:
				inhib_exp += rand(0,2)
				'You seem to have misplaced your clothes! You have little choice but to sneak out the woods, hiding your naked body in shadow and moving toward your grandparents house. You wonder where you might have left your clothes.'
			elseif clothesAtLocation = 1:
				'You decide to stash your belongings and sneak out the woods completely naked, purely for the thrill of it, hiding your naked body in shadow and moving toward your grandparents house, avoiding any well lit areas.'
			end
			act 'Run back home naked':
				minut += 30
				gt'gadforest', 'caught'
			end
		elseif $clothingworntype = 'cheap' and clothingwornnumber = 414:
			if clothesAtLocation = 1:
				'You should pickup your clothes and get dressed before you leave'
			end
		else
			minut += 30
			gs 'stat'
			gt 'gadroad'
		end
	end

	gs 'treeCircEntry', 'GadForest'

	! can only be naked if successfully complete Mira gadforest_event, nude_event
	if $clothingworntype ! 'nude' and clothesAtLocation = 0 and temper >= 15 and sunWeather = 1 and hour>=6 and hour<=20 and miratalkforest > 1 and MiraLoc ! 3 and (pcs_inhib >= 25 or exhibitionist_lvl > 1):
		act'Strip naked':
			if forestpicnic = 0:
				cla
				*clr
				menu_off = 1
				if pcs_inhib < 50:inhib_exp += rand(1,3)
				minut += 5
				if CloSkirtShortness = 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/nudeforest.jpg"></center>'
				if CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/nudeforest1.'+rand(1,2)+'.jpg"></center>'
				gs 'underwear', 'remove'
				gs 'clothing', 'strip', 'gadforest'
				gs 'exhibitionism', 3
				gs 'stat'

				'You don''t hesitate and quickly get undressed, your clothes are left laying in a pile.'

				act'Continue':gt'gadforest','1'
			else
				cla
				*clr
				'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/noundress.jpg"></center>'
				'You see a number of people nearby and decide not to undress right now.'

				act'Continue':gt'gadforest','1'
			end
		end
	end

	if clothesAtLocation = 1 and $clothingworntype = 'nude':
		act 'Get dressed':
			cla
			*clr
			menu_off = 1
			minut += 5
			gs 'clothing', 'swamp_clothes', 0
			gs 'underwear', 'wear'
			gs 'clothing', 'recover_lost_clothes', 'gadforest', 1
			gs 'stat'
			if CloSkirtShortness = 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/nudeforest.jpg"></center>'
			if CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/nudeforest1.'+rand(1,2)+'.jpg"></center>'
			'You change back into your clothes.'
			act 'Back': gt 'gadforest','1'
		end
	end

	! if Sveta left the forest without retrieving her clothes
	if clothesAtLocation = 1 and $clothingworntype ! 'nude':
		if $clothingworntype = 'cheap' and clothingwornnumber = 414:
			act 'Pick up your abandoned clothing':
				cla
				menu_off = 1
				minut += 2
				gs 'clothing', 'swamp_clothes', 0
				gs 'underwear', 'wear'
				gs 'clothing', 'recover_lost_clothes', 'gadforest', 1
				gs 'stat'
				*nl
				'You pick up your abandoned clothing and change out of the hessian sack the hunters gave you.'
				act 'Back': gt 'gadforest','1'
			end
		else
			act 'Pick up your abandoned clothing':
				cla
				menu_off = 1
				minut += 5
				gs 'clothing', 'swamp_clothes', 0
				gs 'underwear', 'wear'
				gs 'clothing', 'recover_lost_clothes', 'gadforest'
				gs 'stat'
				*nl
				'You pick up your abandoned clothing.'
				act 'Back': gt 'gadforest','1'
			end
		end
	end

	if hour >= 6 and hour <= 20 and sunWeather = 1:
		act 'Go for a walk in the woods':
			cla
			*clr
			menu_off = 1
			minut += 60
			pcs_mood += 20
			if $clothingworntype = 'nude':
				pcs_health += healthmax/5 & pcs_horny += 5
				inhib_exp += rand(1,5)
			end
			gs 'stat'

			if month >= 4 and month <= 10:
				if MiraLoc ! 3:
					if ($clothingworntype ! 'nude' and $clothingworntype ! 'swimwear') and CloSkirtShortness = 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest0.'+rand(1,2)+'.jpg"></center>'
					if ($clothingworntype ! 'nude' and $clothingworntype ! 'swimwear') and $pantyworntype ! 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest1.'+rand(1,3)+'.jpg"></center>'
					if ($clothingworntype ! 'nude' and $clothingworntype ! 'swimwear') and $pantyworntype = 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest_tanga0.'+rand(1,2)+'.jpg"></center>'
					if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest_nude0.'+rand(1,4)+'.jpg"></center>'
					'You are walking through the woods, enjoying the sounds of the forest and the fresh air.'
				else
					if $clothingworntype ! 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miraforest'+rand(1,3)+'.jpg"></center>'
					if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miraforest_nude'+rand(1,3)+'.jpg"></center>'
					'You walk with Mira through the woods, enjoying the sounds of the forest and the fresh air.'
				end
			else
				if MiraLoc ! 3:
					'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforestwinter0.'+rand(1,2)+'.jpg"></center>'
					'You walk through the snowy woods, enjoying the sounds of the forest and the fresh air.'
				else
					'<center><img <<$set_imgh>> src="images/characters/village/mira/miraforestwinter.jpg"></center>'
					'You walk with Mira through the snowy woods, enjoying the sounds of the forest and the fresh air.'
				end
			end

			act 'Continue':gt 'gadforest','1'
		end
	end

	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9 and boletus + bilberry < 10 and edge_forestday ! daystart:
		act 'Look for mushrooms and berries':
			cla
			*clr
			menu_off = 1
			minut += 30
			temp_gf = rand(1,2)
			edge_forestday = daystart
			if goforest < 20:goforest += rand(0,1)
			gs 'stat'

			! no mushroom pickers or mushroom pickers moved deeper into the forest
			if mushroom_pickers = 0 or mushroom_pickers = 2:
				cla
				new_boletus += rand(0,1)
				new_bilberry += 1
				boletus += new_boletus
				bilberry += new_bilberry
				pcs_mood += 5
				gs 'stat'

				gs 'gadforest', 'clothes1'
				if new_boletus = 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_bilberry>> kg of berries.'
				if new_boletus > 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms and <<new_bilberry>> kg of berries.'
			end
			! mushroom pickers are at the edge of the forest
			if mushroom_pickers = 1 and temp_gf = 1:
				cla
				new_bilberry += rand(0,1)
				bilberry += new_bilberry
				gs 'stat'

				if new_bilberry > 0: gs 'gadforest', 'clothes1'
				if new_bilberry = 0: gs 'gadforest', 'clothes2'
				if new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_bilberry>> kg of berries.'
				if new_bilberry = 0:'After searching for mushrooms and berries for half an hour you found nothing.'
			end
			if mushroom_pickers = 1 and temp_gf = 2:
				cla
				pcs_mood -= 5
				gs 'stat'

				gs 'gadforest', 'clothes2'
				'After searching for mushrooms and berries for half an hour you found nothing.'
			end
			act 'Continue':gt 'gadforest','1'
		end
 	end

	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9 and MiraLoc ! 3:
		act 'Go further into the forest':
			if goforest >= lostrand:minut += 30 & gt 'gadforest','2'
			if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	 	end
	end

	if joint > 0 and jointhigh = 0: act 'Smoke a joint': gs 'drugs', 'joint' & gt $loc, $metka

	if goswamp = 1 and MiraLoc ! 3:
		! day time chance to get stuck in swamp. ps. use bike to get there 100% of the time by illume
		if hour >= 6 and hour < 20:
			act 'Go to the swamp':
				minut += 75
				if goforest >= lostrand:
					gt 'swamp'
				else
					gt 'gadforestswamp','1'
				end
			end
		! night time chased by wolves
		else
			act 'Go to the swamp':gt 'gadforest_event', 'swamp_road'
		end
	end
	gs 'stat'
end

if $ARGS[0] = 'caught':
	*clr & cla
	menu_off = 1
	caught_rand = rand(0, 100)
	if caught_rand > 90:
		inhib_exp += rand(0,3)
		grandma_trouble += rand(7,10)
		gs 'arousal', 'flash', 10, 'exhibitionism'
		gs 'arousal', 'end'
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadukino_old_woman.jpg"></center>'
		'You''ve been spotted walking home naked by all the women of the village. They are all sitting there scolding you. Your grandparents are definitely going to find out about this.'
	elseif caught_rand > 70:
		inhib_exp += rand(0,4)
		grandma_trouble += rand(1,3)
		gs 'arousal', 'flash', 10, 'exhibitionism'
		gs 'arousal', 'end'
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadukino_old_woman'+rand(1,5)+'.jpg"></center>'
		'You''ve been spotted walking home naked by a woman from the village. You want to hope she doesn''t tell your grandparents.'
	elseif caught_rand > 50:
		inhib_exp += rand(0,3)
		grandma_trouble += rand(1,3)
		gs 'arousal', 'flash', 10, 'exhibitionism'
		gs 'arousal', 'end'
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadukino_old_man'+rand(1,5)+'.jpg"></center>'
		'You''ve been spotted walking home naked by a man from the village. You want to hope he doesn''t tell your grandparents.'
	else
		grandma_trouble += rand(0,1)
		gs 'arousal', 'flash', 10, 'exhibitionism'
		gs 'arousal', 'end'
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/gadukino/grandparents/gaddvor_nude.jpg"></center>'
		'Luckily no one saw you outside naked, but there''s a thrill in almost getting caught.'
	end
	act 'Continue': gt'Gaddvor'
end

if $ARGS[0] = '2':
	*clr & cla
	! slightly inside forest
	$loc = 'gadforest'
	$metka = '2'
	$location_type = 'secluded'

	$menu_loc = 'gadforest'
	$menu_arg = '2'
	menu_off = 0

	gs 'gadukino_event', 'sound'
	gs'stat'

	! Add to knowledge of forest
	if goforest < 40:goforest += rand(0,1)
	! Determine where Sveta got lost in the forest
	lost = 2
	! chance to get lost
	lostrand = rand(1,60)
	foresteventrand = rand(1,10)
	new_boletus = 0
	new_bilberry = 0

	'<center><H4>Forest</H4></center>'
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest2.jpg"></center>'
			'You are in the forest. The fresh air and singing birds light up your soul with joy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_night2.jpg"></center>'
			'You are in the forest. Night in the woods is scary and uncomfortable.'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter2.jpg"></center>'
			'You are in the forest. The forest in the winter is very beautiful, the fresh air makes you happy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter_night2.jpg"></center>'
			'You are in the forest. Night in the forest during wintertime is very scary and uncomfortable.'
		end
	end
	if goforest < 20:'You don''t know this part of the forest very well, it''s best if you don''t go any deeper into the woods.'

	if foresteventrand = 1 and month >= 6 and month <= 9 and mushroom_pickersday ! daystart:mushroom_pickers = 2 & gt 'gadforest_event', 'mushroom_pickers1'
	if foresteventrand = 2 and hour > 8 and hour < 20 and foresteventday ! daystart:gt 'gadforest_event', 'forest_hunters'
	if foresteventrand > 7 and month >= 6 and month <= 9 and foresteventday ! daystart:mushroom_pickers = 2 & gt 'gadforest_event', 'mushroom_pickers3'


	act 'To the edge of the forest':
		lostrand = rand(1,30)
		if goforest >= lostrand:minut += 30 & gt 'gadforest','1'
		if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	end
	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9 and boletus + bilberry < 10 and forestday ! daystart:
		act 'Look for mushrooms and berries':
			cla
			*clr
			menu_off = 1
			minut += 30
			temp_gf = rand(1,2)
			forestday = daystart
			if goforest < 40:goforest += rand(0,1)
			gs 'stat'

			! mushroom_pickers either not seen today or still at the edge of the forest
			if mushroom_pickers < 2:
				cla
				new_boletus += 1
				new_bilberry += rand(1,2)
				boletus += new_boletus
				bilberry += new_bilberry
				pcs_mood += 5
				gs 'stat'

				gs 'gadforest', 'clothes1'
				'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms and <<new_bilberry>> kg of berries.'
			end
			if mushroom_pickers = 2 and temp_gf = 1:
				cla
				new_boletus += rand(0,1)
				new_bilberry += 1
				boletus += new_boletus
				bilberry += new_bilberry
				gs 'stat'

				if new_boletus > 0 or new_bilberry > 0: gs 'gadforest', 'clothes1'
				if new_boletus = 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_bilberry>> kg of berries.'
				if new_boletus > 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms and <<new_bilberry>> kg of berries.'
			end
			if mushroom_pickers = 2 and temp_gf = 2:
				cla
				pcs_mood -= 5
				gs 'stat'

				gs 'gadforest', 'clothes2'
				'After searching for mushrooms and berries for half an hour you found nothing.'
			end
			act 'Continue':gt 'gadforest','2'
		end
	end

	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9:
		act 'Go further into the forest':
			if goforest >= lostrand:minut += 30 & gt 'gadforest','3'
			if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	 	end
	end
end

if $ARGS[0] = '3':
	*clr & cla
	! deep inside forest
	$loc = 'gadforest'
	$metka = '3'
	$location_type = 'secluded'

	$menu_loc = 'gadforest'
	$menu_arg = '3'
	menu_off = 0

	gs 'gadukino_event', 'sound'
	gs'stat'

	! Add to knowledge of forest
	if goforest < 60:goforest += rand(0,1)
	lost = 3
	! Determine where Sveta got lost in the forest
	lostrand = rand(1,100)
	! chance to get lost
	foresteventrand = rand(1,10)
	new_boletus = 0
	new_bilberry = 0

	'<center><H4>Deep Forest</H4></center>'
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest3.jpg"></center>'
			'You are deep in the forest. The fresh air and singing birds light up your soul with joy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_night3.jpg"></center>'
			'You are deep in the forest. Night in the woods is scary and uncomfortable.'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter3.jpg"></center>'
			'You are deep in the forest. The forest in the winter is very beautiful, the fresh air makes you happy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter_night3.jpg"></center>'
			'You are deep in the forest. Night in the forest during wintertime is very scary and uncomfortable.'
		end
	end
	if goforest < 30:'You don''t know this part of the forest very well, it''s best if you don''t go any deeper into the woods.'

	if foresteventrand = 1 and month >= 6 and month <= 9 and foresteventday ! daystart: gt 'gadforest_event', 'basket'
	if foresteventrand = 2 and month >= 6 and month <= 9 and foresteventday ! daystart: gt 'gadforest_event', 'bilberry'
	if foresteventrand = 3 and foresteventday ! daystart: gt 'gadforest_event', 'mushroom'
	if foresteventrand = 4 and hour > 8 and hour < 20 and foresteventday ! daystart:gt 'gadforest_event', 'forest_hunters'

	act 'Go back to the forest':
		lostrand = rand(1,60)
		if goforest >= lostrand:minut += 30 & gt 'gadforest','2'
		if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	end

	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9 and boletus + bilberry < 10 and thicketday ! daystart:

	act 'Look for mushrooms and berries':
			*clr & cla
			menu_off = 1
			minut += 30
			thicketday = daystart
			if goforest < 60:goforest += rand(0,1)
			new_boletus += rand(1,2)
			new_bilberry += rand(1,2)
			boletus += new_boletus
			bilberry += new_bilberry
			pcs_mood += 5
			gs 'stat'
			gs 'gadforest', 'clothes1'
			'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms and <<new_bilberry>> kg of berries.'

			act 'Continue':gt 'gadforest','3'
		end
	end

	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9:

		act 'Go further into the forest':
			if goforest >= lostrand:minut += 30 & gt 'gadforest','4'
			if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	 	end
	end
end

if $ARGS[0] = '4':
	*clr & cla
	! forest near swamp
	$loc = 'gadforest'
	$metka = '4'
	$location_type = 'secluded'

	$menu_loc = 'gadforest'
	$menu_arg = '4'
	menu_off = 0

	gs 'gadukino_event', 'sound'
	gs'stat'

	! Add to knowledge of forest
	if goforest < 80:goforest += rand(0,1)
	lost = 4
	! Determine where Sveta got lost in the forest
	lostrand = rand(1,120)
	! chance to get lost
	foresteventrand = rand(1,10)
	dirty = 0
	new_boletus = 0
	new_bilberry = 0

	'<center><H4>Forest near the Swamp</H4></center>'
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest4.jpg"></center>'
			'You are in the forest near the swamp. The fresh air and singing birds light up your soul with joy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_night4.jpg"></center>'
			'Forest. Night in the woods is scary and uncomfortable.'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter4.jpg"></center>'
			'You are in the forest near the swamp. The forest in the winter is very beautiful, the fresh air makes you happy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter_night4.jpg"></center>'
			'You are in the forest near the swamp. Night in the forest during wintertime is very scary and uncomfortable.'
		end
	end
	if goforest < 60:'You don''t know this part of the forest very well, it''s best if you don''t go any deeper into the woods.'

	if foresteventrand < 2 and month >= 6 and month <= 9 and foresteventday ! daystart: gt 'gadforest_event', 'bilberry'
	if foresteventrand > 7 and month >= 6 and month <= 9 and foresteventday ! daystart: gt 'gadforest_event', 'mushroom'
	if foresteventrand = 5 and hour > 8 and hour < 20 and foresteventday ! daystart:gt 'gadforest_event', 'forest_hunters'

	act 'Go back to the deep woods':
		lostrand = rand(1,100)
		if goforest >= lostrand:minut += 30 & gt 'gadforest','3'
		if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	end
	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9 and boletus + bilberry < 10 and swampforest ! daystart:
		act 'Look for mushrooms and berries':
			cla
			*clr
			menu_off = 1
			minut += 30
			swampforest = daystart
			if goforest < 80:goforest += rand(0,1)
			new_boletus += rand(1,2)
			new_bilberry += rand(2,3)
			boletus += new_boletus
			bilberry += new_bilberry
			pcs_mood += 5
			gs 'stat'

			gs 'gadforest', 'clothes1'
			'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms and <<new_bilberry>> kg of berries.'

			act 'Continue':gt 'gadforest','4'
		end
	end

	act 'Go further into the swamp':
		if goforest >= lostrand and goswamp = 1:minut += 30 & gt 'swamp'
		if goforest >= lostrand and goswamp = 0:minut += 30 & gt 'gadforestswamp','1'
		if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	end
end

!dynamics to ARGS by illume
if $ARGS[0] = 'clothes1':
	if MiraLoc ! 3:
		if $clothingworntype ! 'nude' and CloSkirtShortness = 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/mushroom1.jpg"></center>'
		if $clothingworntype ! 'nude' and $pantyworntype ! 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/mushroom2.jpg"></center>'
		if $clothingworntype ! 'nude' and $pantyworntype = 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/mushroom3.jpg"></center>'
		if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/mushroom4.'+rand(1,3)+'.jpg"></center>'

	else
		if $clothingworntype ! 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miramushroom'+rand(1,2)+'.jpg"></center>'
		if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miramushroom_nude'+rand(1,2)+'.jpg"></center>'
	end
end

if $ARGS[0] = 'clothes2':
	if MiraLoc ! 3:
		if $clothingworntype ! 'nude' and CloSkirtShortness = 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest2.jpg"></center>'
		if $clothingworntype ! 'nude' and $pantyworntype ! 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest3.jpg"></center>'
		if $clothingworntype ! 'nude' and $pantyworntype = 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest_tanga1.'+rand(1,2)+'.jpg"></center>'
		if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest_nude1.'+rand(1,4)+'.jpg"></center>'
	else
		if $clothingworntype ! 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miramushroom3.jpg"></center>'
		if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miramushroom_nude3.jpg"></center>'
	end
end

if $ARGS[0] = 'gadforestloststart':
	cla
	*clr
	menu_off = 1
	gs 'stat'

	'<center><H4>Somewhere in the woods</H4></center>'
	if month >= 5 and month <= 9:
		if $clothingworntype ! 'nude':'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadfear1.jpg"></center>'
		if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadfear2.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadfear3.jpg"></center>'
	end
	'You suddenly realize in horror that you are not familiar with the area you are in... you are lost!.'

	act 'Look for a way out':
		if lost = 1: gt 'gadforestlost','1'
		if lost = 2: gt 'gadforestlost','2'
		if lost = 3: gt 'gadforestlost','3'
		if lost = 4: gt 'gadforestlost','4'
	end

	! add for rescue in forest by illume
	forest_lost = 1
	if StoryLine = 1: forest_lostday = daystart + rand(1,3)
end

--- gadforest ---------------------------------

