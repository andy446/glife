# grandmahelp

menu_off = 1
gs'stat'

frost = 0

'<center><img <<$set_imgh>> src="images/locations/gadukino/village/grandmahelp.jpg"></center>'

'You ask your grandmother if you there was something you could help out with something here at home. Your grandmother turns to you and says:'

if $ARGS[0] = 'start':
	if month <= 4 or month >= 10:
		grandmahelping = rand(0,2)
	elseif month = 5 or month = 6:
		grandmahelping = rand(0,5)
	else
		grandmahelping = rand(0,6)
	end

	if grandmahelping = 0: 
		gt 'grandmahelp', 'cleanfloor'
	elseif grandmahelping = 1:
		gt 'grandmahelp', 'washclothes'
	elseif grandmahelping = 2 and hour < 9 and hour > 20:
		gt 'grandmahelp', 'milkcows'
	elseif grandmahelping = 3:
		gt 'grandmahelp', 'feedchickens'
	elseif grandmahelping = 4 and sunWeather = 1:
		gt 'grandmahelp', 'checkplants'
	elseif grandmahelping = 5 and sunWeather = 1:
		gt 'grandmahelp', 'waterplants'
	elseif grandmahelping = 6 and sunWeather = 1:
		gt 'grandmahelp', 'harvest'
	else
		'"There is nothing to do today <<$pcs_nickname>>, take a day off.'
		act 'Go':gt 'gadhouse', 'start'
	end
end

if $ARGS[0] = 'cleanfloor':
	'"There is always something to do <<$pcs_nickname>>. I''ve gotten quite old and my strength is not what it used to be. Could you help me out by cleaning the floor?"'

	act 'Clean the floor (1:00)':
		*clr & cla
		minut += 60
		grandmahelp += 1
		gs'stat'

		'<center><img <<$set_imgh>> src="images/locations/gadukino/village/washfloor.jpg"></center>'
		'You grab a bucket and a rag, and get down on your knees and begin scrubbing the floor. Within the hour the floor shines and you are satisfied with the work you''ve done. You put the cleaning supplies into a corner and continue on with your day.'
		
		act 'Go':gt 'gadhouse', 'start'
	end
end

if $ARGS[0] = 'washclothes':
	'"Of course, <<$pcs_nickname>>, you can help me wash your grandfathers dirty clothes."'
	
	act 'Wash clothes (1:00)':
		*clr & cla
		minut += 60
		grandmahelp += 1
		gs'stat'

		'<center><img <<$set_imgh>> src="images/locations/gadukino/village/washclothes1.jpg"></center>'
		'You pick up grandfathers dirty clothes and take it with you outside where you soak them in the metal basin for an hour.'
		
		act 'Hang the Laundry':
		    *clr & cla
		    minut += 10
		    gs'stat'
		    '<center><img <<$set_imgh>> src="images/locations/gadukino/village/washclothes2.jpg"></center>'
		    'You are done with the washing and walk over to the yard where you carefully hang the clothes to dry.'

		    act 'Go':gt'Gaddvor'
		end    
	end
end

if $ARGS[0] = 'feedchickens':
	'"Of course <<$pcs_nickname>>, would you be kind and feed the chickens?"'
	
	act 'Go to the yard (0:10)':
		*clr & cla
		feed_chickens = 1
		minut += 10
		gs'stat'

		'<center><img <<$set_imgh>> src="images/locations/gadukino/village/feed_chickens.jpg"></center>'
		'You take the grain mix for the chickens with you and walk over to the yard.'

		act 'Go':gt'Gaddvor'
	end
end

if $ARGS[0] = 'milkcows':
	' "<<$pcs_nickname>>, go to the barn and milk the cows."'

	act 'Go to the barn (0:10)':
		*clr & cla
		minut += 10
		milk_cow = 1
		gs'stat'

		'<center><img <<$set_imgh>> src="images/locations/gadukino/village/go_yard.jpg"></center>'
		'You walk over to the barn where the cows are kept.'

		act 'Further':gt'Gadsarai'
	end	
end

if $ARGS[0] = 'checkplants':
	'"<<$pcs_nickname>>, go to the garden and check on the plants."'

	act 'Go to garden (0:10)':
		*clr & cla
        work_garden = 1
		minut += 10
		gs'stat'

		'<center><img <<$set_imgh>> src="images/locations/gadukino/village/go_yard.jpg"></center>'
		'You go to the garden.'

		act 'Further':gt'Gadgarden','garden'
	end
end

if $ARGS[0] = 'waterplants':
	'"Dear <<$pcs_nickname>>, could you go and water the planted field in the garden?"'

	act 'Go to the garden (0:10)':
		*clr & cla
        water_garden = 1
		minut += 10
		gs'stat'

		'<center><img <<$set_imgh>> src="images/locations/gadukino/village/go_yard.jpg"></center>'
		'You bring a bucket along with you to the garden.'

		act 'Further':gt'Gadgarden','garden'
	end
end

if $ARGS[0] = 'harvest':
	if month = 7:
		'"<<$pcs_nickname>> dear, the strawberries are ripe to pick. Could you go over to the plot and pick them today?"'
		
		act 'Go to the garden (0:10)':
			*clr & cla
			collect_strawberries = 1
			minut += 10
			gs'stat'

			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/go_yard.jpg"></center>'
			'You bring a basket with you and walk to the strawberry plot in the garden.'

			act 'Further':gt'Gadgarden','strawberry'
		end
	elseif month = 8:
		'"<<$pcs_nickname>> dear, the fruits are ripe and it is time to start collecting them, could you go and do it today?"'

		act 'Go to the garden (0:10)':
			*clr & cla
			fruit_collect = 1
			minut += 10
			gs'stat'

			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/go_yard.jpg"></center>'
			'You bring a basket with you and walk to the fruit garden.'

			act 'Further':gt'Gadgarden','fruit_garden'
		end
	elseif month = 9:
		'"<<$pcs_nickname>> dear, could you go to the garden today, it''s time to collect the harvest."'

		act 'Go to garden (0:10)':
			*clr & cla
			harvest_garden = 1
			minut += 10
			gs'stat'

			'<center><img <<$set_imgh>> src="images/locations/gadukino/village/go_yard.jpg"></center>'
			'You bring the harvest tools with you and go over to the garden.'

			act 'Further':gt'Gadgarden','garden'
		end
	end
end

--grandmahelp ---------------------------------

