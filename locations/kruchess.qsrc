# kruchess
$metka = ''
$loc = 'kruchess'
frost = 0
$menu_loc = 'kruchess'
$menu_arg = ''
menu_off = 0
gs'stat'

gs 'themes', 'indoors'

'<center><b><font color = maroon>Chess Club</font></b></center>'
'<center><img <<$set_imgh>> src="images/locations/pavlovsk/community/chess/kruchess.jpg"></center>'
*nl

if hour > 20:
	'Chess club is closed. The coach and some players are tidying away the boards and pieces.'

	act 'Leave':gt'gdkkru'
else
	if week ! 6:
		'The kids are sitting around playing practice games, while being watched over and helped by Coach Genrikh.'

		act 'Spend an hour practicing with assistance from the coach':
			*clr & cla
			chess_exp += rand(0,1)
			intel_exp += rand(0,1)
			minut += 60
			gs 'stat'

			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/community/chess/play' + rand(1,2) + '.jpg"></center>'
			*nl
			'You spend the next hour practicing against the other players with the coach walking around and stopping to help out or point things out. You learn a little every time. You also discuss the various chess openings and which ones are the best, which can often get lively.'

			act 'Return': gt 'kruchess'
		end
		act 'Talk to the other players':
			*clr & cla
			minut += 30
			pcs_mood += 5
			gs 'stat'

			'<center><img <<$set_imgh>> src="images/locations/pavlovsk/community/chess/talk.jpg"></center>'
			*nl
			'You take a break from playing and talk to a couple of your fellow chess enthusiasts. After first focusing on chess for a while, the conversation drifts off to other topics. You have a good time chatting and relaxing.'

			act 'Return': gt 'kruchess'
		end
		act 'Talk to Evgeny Kuznetsov':
			*clr & cla
			minut += 30
			chess_exp += 2
			npc_rel['A151'] += 5
			gs 'stat'

			'<center><img <<$set_imgh>> src="images/pc/npc_headshots/big35.jpg"></center>'
			*nl
			'You sit down next to Evgeny to talk. At first you try to catch his interest with ordinary things; he barely even responds to you, preoccupied playing against himself. Then you bring up a recent opening you have been trying and he perks up. Soon the two of you are talking about various openings and he shares with you some of his favorite ones and how he moves them to the mid game. You learn a bit about chess and feel like you are better friends with Evgeny.'

			act 'Return': gt 'kruchess'
		end
	else
		'On Saturdays, after a week''s training, club members spend the whole day playing against each other.'
		*nl
		if chess_win < 5:
			'You have beaten Vitalik '+str(chess_win)+' times.'
		elseif chess_win >= 5 and chess_win < 10:
			'You have beaten Vitalik 5 times. You cannot play against him anymore.'
			'You have beaten Kostya '+str(chess_win-5)+' times.'
		elseif chess_win >= 10 and chess_win < 15:
			'You have beaten Vitalik 5 times. You cannot play against him anymore.'
			'You have beaten Kostya 5 times. You cannot play against him anymore.'
			'You have beaten Sergei '+str(chess_win-10)+' times.'
		elseif chess_win >= 15 and chess_win < 20:
			'You have beaten Vitalik 5 times. You cannot play against him anymore.'
			'You have beaten Kostya 5 times. You cannot play against him anymore.'
			'You have beaten Sergei 5 times. You cannot play against him anymore.'
			'You have beaten Coach Genrikh '+str(chess_win-15)+' times.'
		elseif chess_win >= 20:
			'You have beaten Vitalik 5 times. You cannot play against him anymore.'
			'You have beaten Kostya 5 times. You cannot play against him anymore.'
			'You have beaten Sergei 5 times. You cannot play against him anymore.'
			'You have beaten Coach Genrikh 5 times. You cannot play against him anymore.'
			'You have beaten Evgeny Kuznetsov '+str(chess_win-20)+' times. He is the only opponent left you can challenge.'
		end
		'You have won a total of '+str(chess_win)+' chess matches.'
		*nl

		if chess_win < 5:
			'You are paired against Vitalik, the youngest boy in the club. Despite his age he has already achieved rank 4, the lowest official chess rank corresponding to an ELO rating of 1000+.'

			act 'Play chess with Vitalik':
				*clr & cla
				$chess_name='Vitalik'
				chessV=10
				gt 'chessplay'
			end
		elseif chess_win < 10:
			'You get to play against Kostya, who recently advanced to rank 3 after passing 1400 ELO points.'

			act 'Play chess with Kostya':
				*clr & cla
				$chess_name='Kostya'
				chessV=30
				gt 'chessplay'
			end
		elseif chess_win < 15:
			'You sit down to play Sergei. You recall that he is a rank 2 player with an ELO rating of more than 1600.'

			act 'Play chess with Sergei':
				*clr & cla
				$chess_name='Sergei'
				chessV=50
				gt 'chessplay'
			end
		elseif chess_win < 20:
			'You are facing Coach Genrikh. At his peak he achieved an ELO ranking of just over 2000, and despite his skills declining with age he still plays at the level of a rank 1 player.'

			act 'Play chess with Coach Genrikh':
				*clr & cla
				$chess_name='Coach Genrikh'
				chessV=70
				gt 'chessplay'
			end
		elseif chess_win >= 20:
			'You are challenging Evgeny Kuznetsov. He has already won the regional championship twice and placed well at the nationals each year he has attended. He is already rated above a 2200 in the ELO rankings.'

			act 'Play chess with Evgeny':
				*clr & cla
				$chess_name='Evgeny'
				chessV=90
				gt 'chessplay'
			end
		end
	end

	act 'Leave':gt 'gdkkru'
end

--- kruchess ---------------------------------

