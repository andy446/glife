# mod_setting

!'Mod system 1.0 by illume'
gs 'obj_din', 'settingtabs'
cla

if settingmode = 1:
	!value= 2, call from Character creation
	act '<center><b>Return to character creation</b></center>': killvar 'menu_page' & gt 'begin', 'start'
else
	!call from Menu - Game settings
	act 'Exit the menu': killvar 'menu_page' & gt 'obj_din', 'menu_exit'
end

*nl '<center><h1>MODs</h1></center>'
'<center>'
'<b>WARNING</b>: Game slowdown may occur when installing too many mods, or mods with bad structure.'
if mod_list > 0:

	'<table border=1><tr><td>id</td><td>Name</td><td>Version</td><td>Author</td><td>Description</td></tr>'
	mod_i=1
	:mod_alllist
	if $mod_name[mod_i] ! '':
		gs 'mod_<<$mod_name[mod_i]>>_setup'
		*p '<tr><td><<mod_i>></td><td><<$mod_info[0]>></td>'
		!name
		*p '<td><<val($mod_info[1])/10000>>.<<(val($mod_info[1]) mod 10000)/100>> <<iif(val($mod_info[1]) mod 100 > 0, ''fix <<val($mod_info[1]) mod 100>> '', '' '')>></td>'
		!version
		*p '<td><<$mod_info[2]>></td>'
		!author
		*p '<td><a href="exec:gs ''mod_<<$mod_info[0]>>_readme''"><img src="images/system/icon/anptekainfo.jpg"></a> <<$mod_info[3]>></td></tr>'
		!description and readme
	else
		'ERROR loading mod id <<mod_i>>, no mod name available'
	end

	if mod_list > mod_i:
		mod_i+=1
		jump 'mod_alllist'
	end
	killvar 'mod_info'
	'</table>'
else
	'<br>You have no Mods activated.'
end
'</center>'
act 'Install new mod':
	killvar 'mod_info'
	$mod_temp=input('input mod name (as for the example image above, input <B>isample</B>)')
	if $mod_temp='':exit
	ADDQST 'mod/<<$mod_temp>>.qsp'
	gs 'mod_<<$mod_temp>>_setup'
	if $mod_info[0] = '':
		msg '<b><<$mod_temp>>.qsp</b> is not a Girl life ECV mod, please contract its author for help'
		exit
	end
	mod_list += 1
	$mod_name[mod_list] = $mod_info[0]
	$mod_version[mod_list] = $mod_info[1]
	$mod_author[mod_list] = $mod_info[2]
	$mod_desc[mod_list] = $mod_info[3]
	gt curloc
end
act 'Disable all mods':
	if input('Are you sure? ALL mod will be disabled!<br>please input <b>qqq</B> to confirm') = 'qqq':
		KILLQST
		set mod_list=0
		KILLVAR '$mod_name'
		KILLVAR '$mod_version'
		KILLVAR '$mod_author'
		KILLVAR '$mod_desc'
		'All mod disabled. You can re-install them later.'
		cla
		act 'Continue':goto curloc
	end
end
--- mod_setting ---------------------------------

