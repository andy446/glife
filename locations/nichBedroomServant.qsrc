# nichBedroomServant

CLOSE ALL

$metka = ''
$loc = 'nichBedroomServant'
$locM = $loc
$nichLoc = 'servant'
$metkaM = ''
$location_type = 'private'
$locclass = 'bedr'
$menu_loc = 'nichBedroomServant'
$menu_arg = ''
menu_off = 0

tanhouse1 = 1

!! Has to happen before there is any output
if nichWork = 2:
	if nichLastWorkDay ! daystart: gs 'nichUtil', 'startWorkday'
	!! this code has to be include everywhere where PC can change clothes or enter the apartment
	gs 'nichUtil', 'checkOutfit'
end

!! Argument return: PC has been in the room before, dont perform actions meant to be executed before or as she enters the room.
if $ARGS[0] = '' or $ARGS[0] = 'start' or $ARGS[0] = 'return':
	gs 'stat'
	'<center><b>Your Bedroom in Nicholas'' Apartment</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/city/citycenter/nichApartment/bedroomServant.jpg"></center>'

	'Your bedroom is fairly large, considering you''re only an employee. There is a small <a href="exec:gt ''bed'', ''start''">double bed</a> and an <a href="exec:gt ''budilnik'', ''start''">alarm clock</a>.'
	'Half a wall is taken up by an expensive-looking <a href="exec:gt ''wardrobe'', ''start''">wardrobe</a> (where you can choose outfits and organize your clothing). There''s a make-up table with a <a href="exec:gt ''mirror'', ''start''">mirror</a> next to it.'
	if komp = 1:
		$komp = ' with your <a href="exec:gt ''Komp'', ''start''">laptop</a>.'
	else
		$komp = '.'
	end
	'A luxurious <a href="exec:gt ''divan'', ''start''">leather sofa</a> is also part of the ensemble. Mounted on the wall opposite the sofa is a big flat <a href="exec:gt ''TV'', ''start''">TV</a>. Under the window, you see a brand-new <a href="exec:gt ''nichBedroomServant'', ''desk''">desk</a><<$komp>>'
	if obruch > 0:'<br>Your hula hoop stands in a corner of the room. '
	if skak > 0:'Your jump rope is sitting on a shelf. '
	if bookYog > 0:'A yoga mat is rolled up in a corner next to the wardrobe. '

	if mishka > 0:'On one of the shelves, you''ve placed your <<mishka>> teddy bear.'

	gs 'exercise', 'start'

	if nichSalaryOutstanding > 0:
		*nl
		'On top of your desk lies your paycheck over <<nichSalaryOutstanding>> <b>₽</b>(<a href="exec:gt ''nichBedroomServant'', ''redeemSalary''">redeem</a>).'
	end
	
	act 'Go to the hallway':
		if nichEvtGalaTele1 = 1 and nichGalaOpinion = 1 and (rand(1,100) <= 40 or nichDebug = 1):
			gt 'nichBedroomServant', 'evtBodyguardIntim1'
		elseif nichGalaOpponent = 31 and (rand(1,100) <= 10 or nichDebug = 1):
			!!Todo: Sveta gets abducted
		else
			if $clothingworntype ! 'nude':
				minut += 1
				gt 'nichApartment'
			else
				msg'<b><font color = red>You probably shouldn''t walk around naked, especially not at your workplace. You need to get dressed.</font></b>'
				gt $CURLOC
			end
		end
	end

	act 'Go to the servant bathroom':
		gt 'nichBathServant'
	end

	act 'Study (2:00)':
		*clr & cla
		menu_off = 1
		minut += 120
		intel_exp += 1
		'<center><img <<$set_imgh>> src="images/pc/activities/study.jpg"></center>'
		'You spend two hours studying and feel a little smarter now.'

		act 'Finish':gt $loc
	end

	if student > 0 and kursovik < 100:
		act 'Write course work (1:00)':
			*clr & cla
			menu_off = 1
			minut += 60
			kursovik += rand(pcs_intel / 20, pcs_intel / 10)

			if kursovik > 100:kursovik = 100

			pcs_mood -= 10
			'<center><img <<$set_imgh>> src="images/pc/activities/study.jpg"></center>'
			'You work on your course work and have now completed <<kursovik>> percent.'

			act 'Finish':gt $loc
		end
	end

	gs 'events', 'read'

	if pcs_magik >= 6 and spellbefshild = 1 and tobiQW = 0:gt'tobiQW','start'

	if sick >= 1:
		act 'Get some bed rest (illness)':
			*clr & cla
			menu_off = 1
			minut += 240
			sick -= 1
			if sick < 0:sick = 0
			gs'stat'

			'<center><img <<$set_imgh>> src="images/locations/shared/apartment/event/sick/sick'+rand(11,17)+'.jpg"></center>'

			'You lie in bed, coughing, sneezing, blowing your nose... and, as if that wasn''t enough, you also think you might be getting a headache.'
			'Drifting in and out of sleep, you spend about 4 hours tossing and turning, trying to sweat out the illness, and you actually feel better when you decide to stretch your legs.'

			act 'Get up': gt 'bedr'
		end
	end

	if pcs_horny >= 50: act 'Masturbate': gt'selfplay', 'start'

	if pornMagazine > 0:'You hide a porn magazine under your mattress.' & dynamic $d_read_porn

	if dur > 0:'You have hidden <<dur>> doses of Pale Lady behind your wardrobe.'

	if dur > 0 and narkday ! daystart:
		act 'Take some cocaine (0:05)':
			*clr & cla
			menu_off = 1
			minut += 5
			narkday = daystart
			gs 'drugs', 'cocaine'
			'<center><img <<$set_imgh>> src="images/shared/drugs/dur.jpg"></center>'
			'You take a small dose of coke and not long after, you start to feel great, as if you could do anything, <b>be</b> anything - but you''re also starting to feel incredibly horny.'

			act 'Leave':gt $curloc
		end
	end
	
	''
	'On the wall next to your desk hangs todays work schedule:'
	if nichVactionTil = daystart:
		'<b>You don''t have to work today.</b>'
	elseif nichVactionTil > daystart:
		'<b>You don''t have to work today</b>. The next '+(nichVactionTil-daystart)+' days are also free.'
	else
		'You should start preparing breakfast before 7:15 during the week and before 8:15 on weekends.'
	end
	
	if nichWork = 2:
	
		gs 'nichChore','inspect','servant'
	
	end
	
elseif $ARGS[0] = 'evtBodyguardIntim1':
	*clr & cla
	'<center><img <<$set_imgh>> src="images/characters/city/taras/encounter1.jpg"></center>'
	'Before you have the chance to leave your room you get pushed back inside by a man you don''t know yet.'
	'Taken by surpise you can only gasp as he painfully turns your right arm around, forcing you into a bent over position.'
	'He obviously knows what he is doing. He has expertly fixated you with just one hand, leaving you next to no room to move.'
	'"Let me introduce myself. My name is Taras, but you will call me Sir. I am the bodyguard of Mistress Gala. I am responsible for protecting not only her life and health, but also the good reputation of her family."'
	'"She told me that her husband hired a little skank as his new maid."'
	'You gasp in surprise as he pulls up your skirt leaving your butt exposed.'
	'"And from the looks of it she is right. Not that I would have doubted her word."'
	'He smacks your left buttock painfully.'
	'"Of course that can''t be accepted."'
	'He smacks your other buttock.'
	'"Consider this your first and only warning. If you whore around this house, if you make Mistress Gala unhappy in any way, you will regret it."'
	'He smacks your left buttock again.'
	'"Do you understand?"'
	!! Todo: Disagree
	'Unable to resist in you current position you meekly nod your head "Yes."'
	'The right buttock again.'
	'"Yes what?"'
	'"Yes sir, I understand."'
	'He waits for another few seconds before gently stroking your hair and then releasing you.'
	'"Good girl. I would hate to actually hurt you." with this obvious lie he leaves your room.'
	gs 'pain', 3, 'asscheeks', 'spank'
	nichGalaOpinion = 2
	act 'Return':
		gt $loc
	end
elseif $ARGS[0] = 'desk':
	if nichGalaOpponent = 21:
		'<center><img <<$set_imgh>> src="images/characters/city/gala/necklace.jpg"></center>'
		'You open your desk. Something doesn''t seem to be right. After closer investigation you find Galas missing necklace hidden under some of your items.'
		act 'Leave it there':
			gt 'stol','start'
		end
		act 'Bring it back to where it belongs':
			minut += 3
			nichGalaOpponent = 22
			gt 'nichBedroomServant', 'return'
		end
	else
		gt 'stol','start'
	end
elseif $ARGS[0] = 'redeemSalary':
	*clr & cla
	karta += nichSalaryOutstanding
	nichSalaryOutstanding = 0
	'Your salary of <<nichSalaryOutstanding>> <b>₽</b> is transferred to your bank account.'
	act 'Back': gt 'nichBedroomServant', 'return'

elseif $ARGS[0] = 'sleepEvents':
	if ARGS[1] = 0:
		if nichGalaOpponent = 30 and hour <= 23 and hour >= 19 and nichDebug = 1:
			!! Start abduction
			!! TODO
			gt 'nichBedroomServant','sleepEvents', 1000
		elseif nichGalaContractTaras = 1 and nichGalaContractTarasLast + rand(3,10) <= daystart:
			!! Taras visits
			gt 'nichBedroomServant','sleepEvents', 100
		end
	elseif ARGS[1] = 100:
		nichRand = rand(1,1)
		if nichRand = 1:
			'<center><img <<$set_imgh>> src="images/characters/city/gala/slave/taras/bed7.jpg"></center>'
			'There is a sudden pain in your ass cheek and you are instantly awake. Somebody has smacked your ass.'
			'Clearing your senses you realize that not only your mouth is shut by some sort of sticking plaster. Also your wrists and ankles are chained to each other with short chains.'
			'In front of your bed you see Taras.'
			'"Sorry, slut. I don''t have time for you. So you have to enjoy this substitute."'
			'He pushes a vibrator into your vagina and sets it to the max setting.'
			gs 'arousal',30,'vaginal','bound'
			'After about half an hour Taras returns and removes the vibrator.'
			'"I know you prefer the real deal. Don''t worry, it won''t take long before you can experience it again."'
			'He unties you and leaves your room.'
			act 'Back to sleep': gt 'bed2','dream'
		end
	elseif ARGS[1] = 1000:
		hour = 23
		minut += 180
		'In the middle of the night you wake up from your sleep. You have the feeling as if somebody is in your room watching you. You look around but there is nobody to be seen.'
		*nl
		'Maybe you are just paranoid?'
		*nl
		'You decide to get back to sleep. You close your eyes and rest your head back on your pillow.'
		*nl
		'Suddenly you feel a strong hand grabbing your face, sealing your mouth and nose shut while another hand pins you down by your hip.'
		*nl
		'You try to scream put you can''t get a single sound out. The last thing you notice before you lose consciousness is a sweetish smell.'
		!!WIP Notice
		*nl
		'<font color = red><b>Warning: This part of the story is currently not finished and most likely ends in a dead end. Click ignore to pass it.</b></font>'
		act 'Everything turns black':
			gt 'nichTaras','abdIntro'
		end
		act 'Ignore':
			nichGalaOpponent = 31
			menu_off = 0
			InSleep = 0
			gt 'nichBedroomServant'
		end
	end	
end

--- nickBedroomServant ---------------------------------