# pavCommercial

CLOSE ALL

$metka = ''
$loc = 'pavCommercial'
$location_type = 'public_outdoors'
$metkaStreet = ''
$locStreet = 'pavCommercial'
$menu_loc = 'pavCommercial'
$menu_arg = ''
menu_off = 0

if sound = 0:
	if month >=11 and month <= 12 or month >=1 and month <=3:
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/zima.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
	else
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street_leto.mp3',30
		if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
	end
end

cla
*clr
litltown = 1
alkoblock = 0
boynumBlock = 0
!!sets the return location for bank and post office
bankloc = 0

!call random events from [street_events], and display them before show location.
if streetevent_hour + 2 < hour:gt'street_event','pavCommercial', 'main'

'<center><h2>Pavlovsk</h2></center>'
if month >= 11 or month <= 3:
	if hour >= 8 and hour <= 21:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/gorodokw.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/pavbuswn.jpg"></center>'
	end
else
	if hour >= 8 and hour <= 21:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/gorodok.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/pavbusn.jpg"></center>'
	end
end
*nl
'Pavlovsk can be split into four regions, the commercial hub here, the residential suburbs, the industrial region and the Five Eight estate of Soviet era apartment blocks.'
*nl
'While the commerical hub of Pavlovsk is better maintained and a bit cleaner looking than other areas, it is still very much rural like the rest of the town, and even from here, you can still easily access the old <a href="exec:view''images/locations/pavlovsk/palace/pav_palace.jpg''">Imperial Palace</a> built by Catherine the Great, and the remodeled <a href="exec:view''images/locations/pavlovsk/fortress/bip_hotel.png''">Bip fortress</a> which now operates as a luxury hotel and restaurant.'
*nl
'A short distance away you can clearly see the golden domes of the <a href="exec:minut += 10 & gt ''ParChurch'',''start''">Church of St Nikolas</a>. It''s only a 10 minute walk to get there from here.'
*nl
'The nearby residential area is next to the lake and the town surrounds the old palace grounds which are now a large <a href="exec:minut += 5 & gt ''gskver'', ''start''">public park</a>.'

gs'street_event','pavCommercial', 'extras'

if AlbFather >= 1:
	if AlbFaterBlackTime = 0:AlbFaterBlackTime = totminut + 2880

	'You can see election posters on the walls all over town. They say: "Vote for Boris Barlovsky!" Wait a minute... isn''t that Albina''s father?'
end

if AlbFaterBlackTime > 0 and AlbFaterBlackTime <= totminut and hour >= 14 and AlbFather = 1:gt 'AlbFatherQW'
if hour >= 14 and hour <= 22 and AlbFather = 2 and AlbZaDay + 30 < daystart and year > 2015:
	gt 'AlbStarsEnd'
end

if StoryLine > 0 and hour < 4 and motherWorry = 0 and SchoolAtestat = 0 and age < 18 and home_owned[2] = 1:
	motherWorry = 1
end

if rand(1, 5) >= 4 and pav_slut >= 50:
	if pav_slut < 75:
		'<br>You catch people staring at you intently, as if they''re supposed to know who you are but can''t quite remember why. You sincerely hope they don''t realize your promiscuous adventures are the reason why. The occasional person chuckling and pointing at you crushes those hopes, and you quickly keep walking before they get a chance to say anything.'
	elseif pav_slut < 100:
		'<br>People look at you with lewd grins on their faces, some of them even making vulgar gestures. Looks like they know what you''ve been up to.'
	else
		'<br>Everywhere you go, people recognize you as a whore. Some even come over and slap you on the ass, claiming they know you like it when they do, or claiming they want to sample your goods. An elderly woman sitting on a bench calls out to you, loudly calling you a slut and a whore. You hide your face and run away as fast as you can, before she can draw too much attention to you.'
	end
end

gs 'stat'

if car > 0 and cardrive = 24:
	cardrive = 24
	'Your <a href="exec:gs ''carF'', ''start''">car<<$car>></a> is parked in the street.'
end

if hour >= 8 and hour <= 20:
	$Pharmname = 'For easy access, there''s even a <a href="exec: minut += 5 & gt ''gaptek'', ''start''">pharmacy</a> attached to it.'
else
	$Pharmname = 'For easy access, there''s even a pharmacy attached to it but it''s currently closed for the day.'
end

'<br>Centered along the main street is the <a href="exec:minut += 15 & gt ''pavtrain''">train station</a> that is always open and running. For those with a car, the end of the street leads to a <a href="exec:minut += 5 & gt ''zaprF'', ''start''">gas station</a> which is usually the last stop people make before heading out onto the nearby highway.'

if week < 6 and hour >= 9 and hour <= 18:
	$desc_txt4 = '<a href="exec:minut += 3 & gt ''postoffice'', ''start''">post office</a>'
elseif week = 6 and hour >= 10 and hour <= 18:
	$desc_txt4 = '<a href="exec:minut += 3 & gt ''postoffice'', ''start''">post office</a>'
else
	$desc_txt4 = '<a href="exec:msg ''post office, which is closed right now. It will reopen again at 9:00 and at 10:00 on saturdays.''">post office</a>'
end

if (week < 6 and hour >= 9 and hour <= 16) or (week = 6 and hour >= 9 and hour <= 13):
	$desc_txt5 = '<a href="exec:gt ''bank'', ''start''">VTB Bank</a>'
else
	$desc_txt5 = '<a href="exec:msg ''VTB Bank, which is closed right now. It will reopen again at 9:00''">VTB Bank</a>'
end

'<br>On the street across from the station is a small <a href="exec:minut += 5 & gt ''gpoli''">Avicenna clinic</a>. <<$Pharmname>> The building next door is shared by the local <<$desc_txt4>> and a <<$desc_txt5>>.'

if hour >= 8 and hour <= 20:
	$desc_txt3 = '<a href="exec:minut += 2 & gt ''shop'', ''start''">Pyaterochka</a>, a popular discount supermarket chain'
else
	$desc_txt3 = '<a href="exec:msg ''Pyaterochka, which is closed right now. It will reopen again at 8:00''">Pyaterochka</a>'
end
'<br>Of course, right after you visit the bank, you can spend your money at <<$desc_txt3>>.' 

if hour >= 8 and hour <= 17:
	$desc_txt1 = '<a href="exec: minut += 3 & gt ''barbershop'', ''start''">A Cut Above</a>, the only barber shop in the area'
else
	$desc_txt1 = '<a href="exec:msg ''A Cut Above, which is closed right now. It will reopen again at 8:00''">A Cut Above</a>'
end
'<br>The rest of the street is filled with smaller locally owned businesses, most notable being <<$desc_txt1>>.'

if leonidSlave = 1:
	'<br>Near the town hall there is an old <a href="exec:minut += 15 & gt ''pavCommercialOffices''">office building</a>.'
end

if hour >= 7 and hour <= 18:
	$desc_txt2 = 'a <a href="exec:minut += 15 & gt ''etoexhib'', ''pos20''">vocational school</a>, where young adults can learn a trade.'
else
	$desc_txt2 = '<a href="exec:msg ''vocational school, which is closed right now. It will reopen again at 7:00''">vocational school</a>.'
end

'<br>If you continue past the shops, you''ll eventually find a <<$desc_txt2>>.'

if workPTU > 2:act '<b>Go to the hostel</b>':gt 'etoexhib', 'pos40'

act 'Walk to the residential area':minut += 5 & gt 'pavResidential'
act 'Walk to the Industrial region':minut += 5 & gt 'pavIndustrial'
act 'Walk to the Five Eight estate':minut += 2 & gt 'pavcomplex', 'start'
act 'Walk to the train station':minut += 15 & gt 'pavtrain'
act 'Walk to the city highway':minut += 15 & nroad = 20 & gt 'road'

act 'Wait':gs 'obj_din', 'wait'

if rand (0,2) ! 0:gs 'din_bad' & dynamic $mobile_check

if npc_QW['A113'] = 1 and daybelisex ! daystart and hour >= 9 and hour <= 23 and week = 7:
	cla & *clr
	daybelisex = daystart
	minut += 5

	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/vadim/belyjeep.jpg"></center>'
	'As you walk through Pavlovsk, Vadim Belys Mitsubishi Pajero stops near you.'

	act 'Go to the jeep': return_residential = 0 & gt 'belgang', 'payday'
elseif npc_QW['A113'] = 1 and daybelisex ! daystart and hour >= 9 and hour <= 23 and week ! 7 and belgangPayWeek >= 300 and belgangProstitute = 1 and rand (0,2) = 0:
	cla & *clr
	daybelisex = daystart
	minut += 5

	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/vadim/belyjeep.jpg"></center>'
	'You see the by now familiar old Mitsubishi Pajero driving through the streets with Vadim Bely and his gang. Your heart skips a beat as you notice one of them pointing at you, and soon they pull over.'

	act 'Go to the jeep': return_residential = 0 & gt 'belgang', 'workofdebt'
end

--- pavCommercial ---------------------------------

