# pavIndustrial

CLOSE ALL

$metka = ''
$loc = 'pavIndustrial'
$location_type = 'public_outdoors'
$metkaStreet = ''
$locStreet = 'pavIndustrial'
$menu_loc = 'pavIndustrial'
$menu_arg = ''
menu_off = 0

if sound = 0:
	if month >=11 and month <= 12 or month >=1 and month <=3:
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/zima.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
	else
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street_leto.mp3',30
		if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
	end
end

cla
*clr
litltown = 1
alkoblock = 0
boynumBlock = 0

!call random events from [street_events], and display them before show location.
if streetevent_hour + 2 < hour:gt'street_event','pavIndustrial', 'main'

'<center><h2>Pavlovsk Industrial District</h2></center>'
'<center><img <<$set_imgh>> src="images/locations/pavlovsk/factory/gshveyfab.jpg"></center>'
*nl
'This is the Industrial region of Pavlovsk, it houses some factories and warehouses along with the old houses that are small and run down.'
*nl
'This area is dominated by one biggest employers in Pavlovsk, the local textile factory. It''s owned by G&M who opened up a store opposite.'

if hour >= 8 and hour < 20 and week >= 1 and week <= 5:
	'Behind the security checkpoint you can see the <a href="exec:minut += 5 & GT ''Gshveyfab'', ''enter''">entrance</a> that every employee passes through.'
elseif hour < 8 or hour > 20 or week > 5:
	'The factory is currently closed.'
end
'Opening hours are Monday through Friday:'
'First shift - 08:00 to 16:00'
'Second shift - 16:00 to 20:00'
*nl
'In a smaller building right next to the factory is the local tailor shop, a service offered by the factory. Opening hours are Monday to Saturday, between 08:00 to 15:00.'

gs'street_event','pavIndustrial', 'extras'

if AlbFather >= 1:
	if AlbFaterBlackTime = 0:AlbFaterBlackTime = totminut + 2880

	'You can see election posters on the walls all over town. They say: "Vote for Boris Barlovsky!" Wait a minute... isn''t that Albina''s father?'
end

if AlbFaterBlackTime > 0 and AlbFaterBlackTime <= totminut and hour >= 14 and AlbFather = 1:gt 'AlbFatherQW'
if hour >= 14 and hour <= 22 and AlbFather = 2 and AlbZaDay + 30 < daystart and year > 2015:
	gt 'AlbStarsEnd'
end

if StoryLine > 0 and hour < 4 and motherWorry = 0 and SchoolAtestat = 0 and age < 18 and home_owned[2] = 1:
	motherWorry = 1
end

if rand(1, 5) >= 4 and pav_slut >= 50:
	if pav_slut < 75:
		'<br>You catch people staring at you intently, as if they''re supposed to know who you are but can''t quite remember why. You sincerely hope they don''t realize your promiscuous adventures are the reason why. The occasional person chuckling and pointing at you crushes those hopes, and you quickly keep walking before they get a chance to say anything.'
	elseif pav_slut < 100:
		'<br>People look at you with lewd grins on their faces, some of them even making vulgar gestures. Looks like they know what you''ve been up to.'
	else
		'<br>Everywhere you go, people recognize you as a whore. Some even come over and slap you on the ass, claiming they know you like it when they do, or claiming they want to sample your goods. An elderly woman sitting on a bench calls out to you, loudly calling you a slut and a whore. You hide your face and run away as fast as you can, before she can draw too much attention to you.'
	end
end

gs 'stat'

if car > 0 and cardrive = 26:
	cardrive = 26
	'Your <a href="exec:gs ''carF'', ''start''">car<<$car>></a> is parked in the street.'
end

if week < 7 and hour >= 8 and hour < 15:
	act 'Visit the tailor': gt 'Gshveyfab', 'tailor'
end

if week < 6 and hour >= 8 and hour < 20:
	*pl 'The local <a href="exec:torg = 0 & GT ''budgetclothes'',''start''">G&M Store</a> is currently open.'
else
	*pl 'The local G&M Store is currently closed. Opening hours are Monday to Friday, between 08:00 to 20:00.'
end

act 'Walk to the residential area':minut += 5 & gt 'pavResidential'
act 'Walk to the Five Eight estate':minut += 2 & gt 'pavcomplex', 'start'
act 'Walk to the commercial area':minut += 5 & gt 'pavCommercial'

act 'Wait':gs 'obj_din', 'wait'

if rand (0,2) ! 0:gs 'din_bad' & dynamic $mobile_check

--- pavCommercial ---------------------------------

