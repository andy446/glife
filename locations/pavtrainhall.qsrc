# pavtrainhall

if $ARGS[0] = '':
	$metka = ''
	$loc = 'pavtrainhall'
	$menu_loc = 'pavtrainhall'
	$menu_arg = ''
	menu_off = 0
	$location_type = 'public_indoors'
	CLOSE ALL
	if sound = 0:PLAY 'sound/vokzal2.mp3',30
	clr
	frost = 0
	poezddalsled = 0
	gs 'stat'
	gs 'themes', 'indoors'

	'<center><h4>Pavlovsk train station interior</h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/trainstation/vokzal.jpg"></center>'
	'The center of the station building is a large waiting room, where passengers can wait until their train arrives. Opposite to that you can find the <a href="exec: gt ''pavtrainhall_events'', ''manager''">station manager''s office</a>, and the <a href="exec:minut += 1 & gt ''pavtrainhall'', ''toilet''">toilets</a> on the other end of the building.'
	'The long-distance trains are at the station platforms between 11:00-11:30 and 18:00-18:30.'

	*nl
	if katalkinSexOnce = 1:'There is a small <a href="exec:minut += 5 & gt ''katalkin''">police station</a> inside the station building.'

	*nl
	if StoryLine = 1 and SchoolAtestat = 0:
		if (sunWeather = 0 or temper <= 0) and hour >= 18 and hour < 20 and week < 6:
			'You see your classmates <a href="exec:minut += 3 & gt ''gopskver''">Vitek, Dan and Vasily</a> hanging around here.'
		end
	end

	if podbros = 0:
		podrand = rand(0, 1)

		if money >= 1000 and hour >= 8 and hour <= 20 and podrand = 0:'As you walk through the station building, you notice a man''s thick <a href="exec: gt ''pavtrainhall_events'', ''wallet1''">wallet</a> dropping from his pocket, not far away from you.'
		*nl
	end

	act 'Leave the train station':minut += 3 & gt 'pavtrain'

	if Trainpass_day > daystart:
		'You have <<Trainpass_day - daystart>> days left on your train pass.'
		act 'Go to the platforms': gt 'pavtrainhall', 'platform'
	else
		act 'Go to the ticket office': gt 'pavtrainhall', 'ticket'
	end

	if snarkozak = 1 and narkossista = 0 and narkozakday!day:
		if money >= 1500:
			act 'Find the drug dealer':gs 'events', 'snarkozaka'
		end
	end

	if vokzalVork >= 1:
		if cleZalDay ! daystart or cleMTDay ! daystart or cleWTDay ! daystart:
			act 'Work': gt 'pavtrainhall', 'work'
		elseif cleZarplatDay ! daystart:
			act 'Collect your salary':
				menu_off = 1
				*clr & cla
				'<center><h4>Pavlovsk train station interior</h4></center>'
				'<center><img <<$set_imgh>> src="images/locations/pavlovsk/trainstation/vokzal.jpg"></center>'
				cleZarplatDay = daystart
				vokzalVork += 1
				money += 130
				minut += 3
				gs 'stat'
				'When you are done cleaning all the places you were told to clean, you go to the cashier and collect your well-earned salary.'
				act 'Continue':minut += 1 & gt 'pavtrainhall'
			end
		end
	end

	*nl
	if hour = 11 and minut = 0:
		poezddalsled = 1
		'The 11:30 long-distance train is currently approaching the platform.'
	elseif hour = 11 and minut > 0 and minut < 30:
		poezddalsled = 1
		'The 11:30 long-distance train is currently waiting at the platform.'
	elseif hour = 11 and minut = 30:
		poezddalsled = 1
		'The 11:30 long-distance train is currently leaving the station.'
	elseif hour = 18 and minut = 0:
		poezddalsled = 1
		'The 18:30 long-distance train is currently approaching the platform.'
	elseif hour = 18 and minut > 0 and minut < 30:
		poezddalsled = 1
		'The 18:30 long-distance train is currently waiting at the platform.'
	elseif hour = 18 and minut = 30:
		poezddalsled = 1
		'The 18:30 long-distance train is currently leaving the station.'
	end

	if poezddalsled = 1:
		'The platform is bustling with passengers that are entering or leaving the currently waiting train, carrying their heavy bags with them. Several police officers are patrolling the platforms while the train is here, keeping an eye out for any unruly activities.'

		if tovarL > 0:
			act 'Sell your goods to passengers':gt 'pavtrainhall_events', 'trinkets'
		end
	end

	act 'Wait':gs 'obj_din', 'wait'
end

if $ARGS[0] = 'platform':
	$loc = 'pavtrainhall'
	$metka = 'platform'
	$menu_loc = 'pavtrainhall'
	$menu_arg = 'platform'
	menu_off = 0
	$location_type = 'public_outdoors'
	CLOSE ALL
	if sound = 0:
		if month >=11 and month <= 12 or month >=1 and month <=3:
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/zima.mp3',30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
		else
			if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street_leto.mp3',30
			if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
		end
	end

	*clr & cla
	gs'stat'

	'<center><H4>Pavlovsk station platforms</H4></center>'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/pavplat.jpg"></center>'
	'Because Pavlovsk is a place that attracts a fair amount of tourists, the station can be quite busy at times. The busiest times on the platforms are between 11:00 and 11:30, and between 18:00 and 18:30 when the long-distance trains are ready for boarding.'

	act 'Return to the train station': gt'pavtrainhall'

	if Trainpass_day > daystart:
		act 'Take a train to the old town': gt 'pavtrainhall', 'oldtown'

		act 'Take a train to the village of Gadukino': gt 'pavtrainhall', 'village'

		act 'Take a train to the the communal village': gt 'pavtrainhall', 'communal'

		act 'Take a train to city industrial area': gt 'pavtrainhall', 'ind'

		act 'Take a train to city center': gt 'pavtrainhall', 'centre'
	end
end

if $ARGS[0] = 'work':
	menu_off = 1
	*clr & cla
	'<center><h4>Pavlovsk train station interior</h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/trainstation/vokzal.jpg"></center>'
	if cleZalDay ! daystart:
		act 'Mop the waiting room floor (1:00)':
			cls
			cleZalDay = daystart
			gs 'exercise', 'tier1', 60, 'stren_exp', 'vital_exp'
			pcs_mood -= 6
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/locations/city/residential/office/clener1.jpg"></center>'
			'You take some cleaning supplies from the maintenance closet, pour some water and soap into a bucket and wash the floor in the waiting room.'

			act 'Continue':minut += 1 & gt 'pavtrainhall', 'work'
		end
	end

	if cleMTDay ! daystart:
		act 'Clean the men''s restroom (0:30)':
			cls
			cleMTDay = daystart
			gs 'exercise', 'tier1', 30, 'stren_exp', 'vital_exp'
			pcs_mood -= 6
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/locations/city/residential/office/clener1.jpg"></center>'
			'You take some cleaning supplies from the maintenance closet, pour some water and soap into a bucket and go to the men''s room.'
			gs 'pavtrainhall_events', 'toilet_event'
			!!'You began to wash the floor in the men''s room, and then began to scrub toilets.'

			act 'Leave restroom':minut += 1 & gt 'pavtrainhall', 'work'
		end
	end

	if cleWTDay ! daystart:
		act 'Clean the women''s restroom (0:30)':
			cls
			cleWTDay = daystart
			gs 'exercise', 'tier1', 30, 'stren_exp', 'vital_exp'
			pcs_mood -= 6
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/locations/city/residential/office/clener1.jpg"></center>'
			'You take some cleaning supplies from the maintenance closet, pour some water and soap into a bucket and scrub the toilets in the ladies'' room before mopping the floor.'
			gs 'pavtrainhall_events', 'vera'

			act 'Leave restroom':minut += 1 & gt 'pavtrainhall', 'work'
		end
	end
	act 'Return': gt 'pavtrainhall'
end

if $ARGS[0] = 'ticket':
	menu_off = 1
	cls
	gs 'stat'
	'<center><h4>Ticket office</h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/kassa.jpg"></center>'

	act 'Leave the ticket office':minut += 1 & gt 'pavtrainhall'

	if money >= 50:
		act 'Buy a ticket':
			cla
			gs 'pavtrainhall', 'buy_ticket'
		end
	end

	if money >= 1500 and Trainpass_day <= daystart:
		act 'Buy a weekly pass (7 days - 1500 <b>₽</b>) ':
			cla
			money -= 1500
			Trainpass_day = daystart + 7
			act 'Return': gt 'pavtrainhall'
		end
	end

	if money >= 5500 and Trainpass_day <= daystart:
		act 'Buy a monthly pass (30day - 5500 <b>₽</b>)':
			cla
			money -= 5500
			Trainpass_day = daystart + 30
			act 'Return': gt 'pavtrainhall'
		end
	end
end

if $ARGS[0] = 'buy_ticket':
	menu_off = 1

	act 'Leave the ticket office':minut += 1 & gt 'pavtrainhall'

	if money >= 50:
		act 'Buy a ticket to the old town (50 <b>₽</b>)':
			money -= 50
			gt 'pavtrainhall', 'oldtown'
		end
	end

	if money >= 75:
		act 'Buy a ticket to the village of Gadukino (75 <b>₽</b>)':
			money -= 75
			gt 'pavtrainhall', 'village'
		end
	end

	if money >= 100:
		act 'Buy a ticket to the the communal village (100 <b>₽</b>)':
			money -= 100
			gt 'pavtrainhall', 'communal'
		end
	end

	if money >= 125:
		act 'Buy a ticket to city industrial area (125 <b>₽</b>)':
			money -= 125
			gt 'pavtrainhall', 'ind'
		end
	end

	if money >= 150:
		act 'Buy a ticket to city center (150 <b>₽</b>)':
			money -= 150
			gt 'pavtrainhall', 'centre'
		end
	end
end

if $ARGS[0] = 'oldtown':
	$loc = 'train'
	$metka = 'oldtown'
	$menu_loc = 'train'
	$menu_arg = 'oldtown'
	menu_off = 0
	minut += 5
	if rand(1, 100) < 20: gt 'train', 'events'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/pushplat.jpg" ></center>'
	'After 5 minutes, the train stops at old platform by the old town of Pushkin. Famous for its extensive park and old historic buildings, the locals are known to be eccentric but that adds to the charm.'
	act 'Get off the train':
		if rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'train', 'oldtown'
	end
end

if $ARGS[0] = 'village':
	$loc = 'train'
	$metka = 'village'
	$menu_loc = 'train'
	$menu_arg = 'village'
	menu_off = 0
	minut += 15
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) < 20: gt 'train', 'events'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/gadplat.jpg" ></center>'
	'After 15 minutes, the train stops at the small station near the village of Gadukino.'
	act 'Get off the train':
		if rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'train', 'village'
	end
end

if $ARGS[0] = 'communal':
	$loc = 'train'
	$metka = 'communal'
	$menu_loc = 'train'
	$menu_arg = 'communal'
	menu_off = 0
	minut += 25
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) <= 20: gt 'train', 'events'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/oldplat.jpg" ></center>'
	'After 20 minutes you arrive at an old railway platform near the communal village.'
	act 'Get off the train':
		if rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'train', 'communal'
	end
end

if $ARGS[0] = 'ind':
	$loc = 'nordtrain'
	$metka = 'platform'
	$menu_loc = 'nordtrain'
	$menu_arg = 'platform'
	menu_off = 0
	minut += 35
	if rand(1, 100) < 15: gt 'train', 'private'
	if rand(1, 100) < 30: gt 'train', 'Gopnik'
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) <= 20: gt 'train', 'events'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/indusplat.jpg" ></center>'
	'After 35 minutes, the train stops at the St. Petersburg city industrial area station.'
	act 'Get off the train':
		if rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'nordtrain', 'platform'
	end
end

if $ARGS[0] = 'centre':
	$loc = 'cityTrain'
	$metka = 'inside'
	$menu_loc = 'cityTrain'
	$menu_arg = 'inside'
	menu_off = 0
	minut += 40
	if rand(1, 100) < 15: gt 'train', 'private'
	if rand(1, 100) < 30: gt 'train', 'Gopnik'
	if rand(1, 100) <= 10: gt 'train_incidental', 'events'
	if rand(1, 100) <= 20: gt 'train', 'events'
	gs 'stat'
	'<center><img <<$set_imgh>> src="images/locations/shared/train/cityplat.jpg" ></center>'
	'After 40 minutes, the train stops at the St. Petersburg city center station. The large office blocks and shopping centers fill your view.'
	act 'Get off the train':
		if rand(1, 100) <= 5: gt 'train_incidental', 'end'
		gt 'cityTrain', 'inside'
	end
end

if $ARGS[0] = 'toilet':
	$menu_loc = 'pavtrainhall'
	$menu_arg = 'toilet'
	menu_off = 0
	cls
	vokzalToilet = 0
	gs 'stat'

	'<center><h4>Pavlovsk train station interior</h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/trainstation/vokzal.jpg"></center>'
	'You are at the station''s restrooms, you can find the <a href="exec:minut += 1 & gt ''pavtrainhall'', ''female''">ladies'' room</a> and <a href="exec:minut += 1 & gt ''pavtrainhall'', ''male''">men''s room</a> here.'

	act 'Return to the train station':minut += 1 & gt 'pavtrainhall'
end

if $ARGS[0] = 'female':
	$locM = 'pavtrainhall'
	$metkaM = 'female'
	$loc = 'pavtrainhall'
	$metka = 'female'
	$menu_loc = 'pavtrainhall'
	$menu_arg = 'female'
	menu_off = 0
	$location_type = 'bathroom'
	cls
	vokzalToilet = 1
	gs 'stat'
	'<center><h4>The ladies'' room</h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/shared/bathroom/publictoilet.jpg"></center>'
	'You open the door to the ladies'' room and go in.'

	act 'Leave':minut += 1 & gt 'pavtrainhall', 'toilet'

	act 'Look in the mirror': gt 'mirror', 'start'

	dynamic $tampon
	dynamic $quickwash
	dynamic $basin
	dynamic $removepan
end

if $ARGS[0] = 'male':
	$locM = 'pavtrainhall'
	$metkaM = 'male'
	$loc = 'pavtrainhall'
	$metka = 'male'
	$menu_loc = 'pavtrainhall'
	$menu_arg = 'male'
	menu_off = 0
	$location_type = 'bathroom'
	cls
	vokzalToilet = 2
	gs 'stat'
	'<center><h4>The men''s room</h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/shared/bathroom/publictoilet.jpg"></center>'
	'You open the door to the men''s room and go in.'

	act 'Leave':minut += 1 & gt 'pavtrainhall', 'toilet'

	if bimbolevel >= 1:
		act 'Wait and see if someone finds you':gt 'trainbimbo', 'pos1'
		act 'Quickly enter a stall':gt 'trainbimbo', 'pos5'
	end

	act 'Look in the mirror':gt 'mirror', 'start'

	dynamic $tampon
	dynamic $quickwash
	dynamic $basin
end

--- pavtrainhall ---------------------------------

