# stol
!{change log
31 March, 2016
Added birth control control options to desk in Pavlovsk and "Bric-a-Brac drawer"
}
if $ARGS[0] = 'start':
	cla
	*clr
	'<center><h1>Desk</h1></center>'
	'<center><img <<$set_imgh>> src="images/shared/home/bedroom/stol.jpg"></center>'
	*nl
	gs 'stat'
	'Bric-a-brac drawer.'

	act 'Check birth control': gt 'stol', 'bc'
	if lubri > 0:'Lubricant, enough for <<lubri>> time(s).'
	if krem > 0:'Sunblock, enough for <<krem>> more application(s).'
	if abonement > 0:'Fitness center subscription, valid for <<abonement>> more visit(s)'
	if dildo > 0:'Realistic Dildo.'

	if mentats_have > 0:
		'<a href="exec:dynamic $use_mentats & gt ''stol'', ''start''"><b>Take neurobooster pill.</b></a> Uses left: <<mentats_have>>. Warning on the box says not to take more than one per day.'
	end
	if steroid_have > 0:
		'<a href="exec:dynamic $use_steroids & gt ''stol'', ''start''"><b>Take steroids.</b></a> Uses left: <<steroid_have>>.'
	end
	if aphrodisiac_have > 0:
		'<a href="exec:dynamic $use_aphrodisiac & gt ''stol'', ''start''"><b>Chew aphrodisiac gum.</b></a> Uses left: <<aphrodisiac_have>>.'
	end

	act 'Close the drawer':
		cla
		gt 'stol', 'fin'
	end

	if money > 0:
		act 'Put some money in the drawer':
			cla
			stolIN = input ("How much money you want to put in the desk drawer? (Your purse contains <<money>> <b>₽</b>.)")

			if stolIN <= 0 or stolIN > money:
				!!'Not correct operation.'
			else
				stolmoney += stolIN
				money -= stolIN
			end

			gt 'stol', 'fin'
		end
	end

	if stolmoney > 0:
		act 'Take some money from the desk.':
			cla
			stolOUT = input ("How much money do you want to put in your purse? (There is <<stolmoney>> <b>₽</b> here.)")

			if stolOUT <= 0 or stolOUT > stolmoney:
				!!'Not correct operation.'
			else
				stolmoney -= stolOUT
				money += stolOUT
				!!'You debited <<kartaOUT>> <b>₽</b>, now on your account <<karta>> <b>₽</b>.'
			end

			gt 'stol', 'fin'
		end
	end
end

if $ARGS[0] = 'fin':
	cla
	gt $loc, $metka
end

if $ARGS[0] = 'stolPar':
	*clr & cla
	gs'stat'
	'<center><h1>Desk</h1></center>'
	'<center><img <<$set_imgh>> src="images/shared/home/bedroom/stol.jpg"></center>'
	*nl
	'You sit down at your desk.'
	
	act 'Get up':gt $loc, $metka
	act 'Check your birth control': gt 'stol', 'bc'
	
	if mentats_have > 0:
		'<a href="exec:dynamic $use_mentats & gt ''stol'', ''stolPar''"><b>Take neurobooster pill.</b></a> Uses left: <<mentats_have>>. Warning on the box says not to take more than one per day.'
	end
	if steroid_have > 0:
		'<a href="exec:dynamic $use_steroids & gt ''stol'', ''stolPar''"><b>Take steroids.</b></a> Uses left: <<steroid_have>>.'
	end
	if aphrodisiac_have > 0:
		'<a href="exec:dynamic $use_aphrodisiac & gt ''stol'', ''stolPar''"><b>Chew aphrodisiac gum.</b></a> Uses left: <<aphrodisiac_have>>.'
	end

	if lernHome > 0:
		act 'Do your homework':
			cla & *clr
			'<center><img <<$set_imgh>> src="images/locations/shared/apartment/homework.jpg"></center>'
			if lernHome <= 2:
				minut += 60*lernHome
				lernHome = 0
				lern += 1
				'It takes you one hour to complete your homework.'
				gs'stat'
			elseif lernHome > 2:
				minut += 60*2
				lernHome = 0
				'It takes you two hours to complete your homework.'
				gs'stat'
			end
			
			act 'Get up from the table':gt $loc, $metka
		end
	end
	
	if money > 0:
		act 'Hide some money in your drawer':
			cla
			stolIN = input ("How much money you want to hide in your drawer? (Your purse contains <<money>> <b>₽</b>.)")

			if stolIN <= 0 or stolIN > money:
				'You can''t put this amount into the drawer.'
			else
				stolmoney += stolIN
				money -= stolIN
			end

			gt 'stol', 'stolPar'
		end
	end

	if stolmoney > 0:
		act 'Take some money from your drawer':
			cla
			stolOUT = input ("How much money do you want to put in your purse? (There is <<stolmoney>> <b>₽</b> here.)")

			if stolOUT <= 0 or stolOUT > stolmoney:
				'You can''t take this amount from your drawer.'
			else
				stolmoney -= stolOUT
				money += stolOUT
				if stolmoney > 0: 'You have <<stolmoney>> <b>₽</b> hiden in your drawer.'
			end

			gt 'stol', 'stolPar'
		end
	end
end

if $ARGS[0] = 'bc':
	*clr & cla
	'<center><h1>Desk</h1></center>'
	'<center><img <<$set_imgh>> src="images/shared/home/bedroom/stol.jpg"></center>'
	*nl
	gs 'stat'
	gs 'din_bad'
	'In the drawer of your desk, you store your birth control.'
	if prezik + prezikcount + preziksabo > 0:
		if preziktype = 2 or preziksabo > 0:
			'You have <<iif(preziktype = 2, prezikcount, prezikcount+prezik)>> normal, and <<iif(preziktype = 2, prezik+preziksabo, preziksabo)>> sabotaged <a href="exec:dynamic $din_Table_Condom_Menu"><b>condoms.</b></a>'
		elseif preziktype < 2 and preziksabo = 0:
			'You have <<prezik+prezikcount>> <a href="exec:dynamic $din_Table_Condom_Menu"><b>condoms.</b></a>'
		END
		*nl
	else
		'You are out of condoms.'
	end
	if tabletki > 0 and pillsleft[ptype] > 0:
		'You have <<tabletki>> unopened packages of <a href="exec:dynamic $d_tabletkiedt">birth control pills</a> and <<pillsleft[ptype]>> pills left in your opened package.</b>'
	elseif tabletki	= 0 and pillsleft[ptype] > 0:
		'You have <<pillsleft[ptype]>> <a href="exec:dynamic $d_tabletkiedt">birth control pills</a> left.</b>'
	elseif tabletki > 0 and pillsleft[ptype] = 0:
		'You have <<tabletki>> unopened packages of <a href="exec:dynamic $d_tabletkiedt">birth control pills</a>.'
	else
		'You are out of birth control pills.'
	end
	act 'Close the drawer':gt $loc, $metka
end

--- stol ---------------------------------

